@Ecore(
	nsURI="http://andycarpenter.work/metamodel/base",
	nsPrefix="base"
)
@Ecore(
	validationDelegates="http://www.eclipse.org/emf/2002/Ecore/OCL",
	settingDelegates="http://www.eclipse.org/emf/2002/Ecore/OCL",
	invocationDelegates="http://www.eclipse.org/emf/2002/Ecore/OCL"
)
@GenModel(
	editDirectory="/work.andycarpenter.metamodel.base.edit/src",
	editPluginID="work.andycarpenter.metamodel.base.edit",
	childCreationExtenders="true")
package work.andycarpenter.metamodel.base

import work.andycarpenter.metamodel.expression.Expression
import work.andycarpenter.metamodel.expression.Literal
import work.andycarpenter.metamodel.expression.Variable

annotation "http://www.eclipse.org/emf/2002/Ecore/OCL" as OCL


class BaseModel {
	contains Literal[] literals
	contains DataType[] dataTypes
}


class NullLiteral extends Literal {
}

class BooleanLiteral extends Literal {
	Boolean[1] value
}

class IntegerLiteral extends Literal {
	int[1] value
}

class StringLiteral extends Literal {
	String[1] value
}

class TimeLiteral extends Literal {
	String[1] value
}


class CurrentTime extends Variable {
}

class CurrentUser extends Variable {
}


abstract class NamedElement {
	String[1] name
}

abstract class NamedDisplayElement extends NamedElement {
	String displayLabel
}


interface ConstantContainer {
	contains Constant[] constants opposite definedBy
}

class Constant extends NamedElement {
	refers ConstantContainer definedBy opposite constants
	contains Expression value
}

class ConstantReference extends Variable { 
	refers Constant[1] value 
} 
 

abstract class Classifier extends NamedDisplayElement {
	@OCL(derivation="if self.oclIsKindOf(DataType) then self.oclAsType(DataType).isBooleanDataType else false endif")
	derived readonly unsettable transient volatile boolean isBooleanClassifier
	@OCL(derivation="if self.oclIsKindOf(DataType) then self.oclAsType(DataType).isEmailDataType else false endif")
	derived readonly unsettable transient volatile boolean isEmailClassifier
	@OCL(derivation="self.oclIsTypeOf(EnumerationType)")
	derived readonly unsettable transient volatile boolean isEnumerationClassifier
	@OCL(derivation="if self.oclIsKindOf(DataType) then self.oclAsType(DataType).isIntegerDataType else false endif")
	derived readonly unsettable transient volatile boolean isIntegerClassifier
	@OCL(derivation="if self.oclIsKindOf(DataType) then self.oclAsType(DataType).isStringDataType else false endif")
	derived readonly unsettable transient volatile boolean isStringClassifier
	@OCL(derivation="if self.oclIsKindOf(DataType) then self.oclAsType(DataType).isTextareaDataType else false endif")
	derived readonly unsettable transient volatile boolean isTextareaClassifier
	@OCL(derivation="if self.oclIsKindOf(DataType) then self.oclAsType(DataType).isUrlDataType else false endif")
	derived readonly unsettable transient volatile boolean isUrlClassifier
}

class DataType extends Classifier {
	String persistentType
	String ormType
	String wafType
	String interfaceType
	String validationPattern = ''
	@OCL(derivation="name = 'Boolean'")
	derived readonly unsettable transient volatile boolean isBooleanDataType
	@OCL(derivation="name = 'Email'")
	derived readonly unsettable transient volatile boolean isEmailDataType
	@OCL(derivation="self.oclIsTypeOf(EnumerationType)")
	derived readonly unsettable transient volatile boolean isEnumerationType
	@OCL(derivation="name = 'Float'")
	derived readonly unsettable transient volatile boolean isFloatDataType
	@OCL(derivation="name = 'Integer'")
	derived readonly unsettable transient volatile boolean isIntegerDataType
	@OCL(derivation="name = 'String'")
	derived readonly unsettable transient volatile boolean isStringDataType
	@OCL(derivation="name = 'Textarea'")
	derived readonly unsettable transient volatile boolean isTextareaDataType
	@OCL(derivation="name = 'URL'")
	derived readonly unsettable transient volatile boolean isUrlDataType
}

class EnumerationType extends DataType {
	contains EnumerationLiteral[] enumerations
}

class EnumerationLiteral extends NamedDisplayElement {
}


interface FormalParameterList {
	contains FormalParameter[] parameters
}

class FormalParameter extends NamedElement {
	refers Classifier[1] dataType
	String defaultValue
	String placeholder = ''
	String description = ''
}

class ParameterReference extends Variable { 
	refers FormalParameter[1] parameter 
} 
@Ecore(
	nsURI="http://andycarpenter.work/metamodel/service",
	nsPrefix="service"
)
@Ecore(
	validationDelegates="http://www.eclipse.org/emf/2002/Ecore/OCL",
	settingDelegates="http://www.eclipse.org/emf/2002/Ecore/OCL",
	invocationDelegates="http://www.eclipse.org/emf/2002/Ecore/OCL"
)
@GenModel(
	editDirectory="/work.andycarpenter.metamodel.service.edit/src",
	editPluginID="work.andycarpenter.metamodel.service.edit",
	childCreationExtenders="true")
package work.andycarpenter.metamodel.service

import work.andycarpenter.metamodel.base.ConstantContainer
import work.andycarpenter.metamodel.base.FormalParameterList
import work.andycarpenter.metamodel.base.NamedElement
import work.andycarpenter.metamodel.orm.OrmModel
import work.andycarpenter.metamodel.orm.Repository

annotation "http://www.eclipse.org/emf/2002/Ecore/OCL" as OCL


class ServiceModel {
	refers OrmModel[1] persistence
	contains Service[] services opposite partOf
}

class Service extends NamedElement, ConstantContainer {
	refers ServiceModel[1] partOf opposite services
	contains BusinessOperation[] operations opposite partOf
	refers Repository[] requires
	refers Service[] uses
	boolean usesVichUploaderService = "false"
	@OCL(derivation="operations->select(o | o.resultType = OperationResultTypes::File)")
	refers derived readonly unsettable transient volatile BusinessOperation[] fileOperations
	@OCL(derivation="fileOperations->notEmpty()")
	derived readonly transient unsettable volatile boolean hasFileOperations
}

enum OperationClass {
	ReadOnly= 0
	Costly = 1
	Modifying = 2
}

class BusinessOperation extends NamedElement, FormalParameterList {
	container Service[1] partOf opposite operations
	refers Service[] uses
	OperationClass operationClass = 'ReadOnly'
	OperationResultTypes[1] resultType
	String resultMimeType = ''
	String fileExtension  = ''
}

enum OperationResultTypes {
	None
	File = 1
}


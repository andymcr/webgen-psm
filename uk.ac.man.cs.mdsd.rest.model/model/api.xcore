@Ecore(
		nsPrefix="api",
		nsURI="http://cs.manchester.ac.uk/mdsd/API"
)
@GenModel(
	editDirectory="/uk.ac.man.cs.mdsd.api.edit/src",
	editPluginID="uk.ac.man.cs.mdsd.api.edit")
package uk.ac.man.cs.mdsd.api

import uk.ac.man.cs.mdsd.orm.SerializationGroup
import uk.ac.man.cs.mdsd.service.Selection
import uk.ac.man.cs.mdsd.service.Service


class API {
	contains Resource[] resources
}

class Resource {
	container Resource parentResource opposite childResources
	refers Service[1] service
	derived String[1] name get {
		if (service != null)
			service.name
	}
	String[1] uriElement
	boolean enableDefaultGetOne = "true"
	boolean enableDefaultGetAll = "true"
	refers Selection getAll
	refers Selection getOne
	contains ResourceSelection[] selections opposite parentResource
	refers SerializationGroup[] defaultSerializationGroups
	contains Resource[] childResources opposite parentResource
}

class ResourceSelection {
	container Resource parentResource opposite selections
	refers Selection[1] selection
	derived String[1] name get {
		if (selection != null)
			selection.name
	}
	String[1] uriElement
	refers SerializationGroup[] defaultSerializationGroups
}
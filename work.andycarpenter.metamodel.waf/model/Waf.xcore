@Ecore(
	nsURI="http://andycarpenter.work/metamodel/WebApplicationFramework",
	nsPrefix="waf"
)
@Ecore(
	validationDelegates="http://www.eclipse.org/emf/2002/Ecore/OCL",
	settingDelegates="http://www.eclipse.org/emf/2002/Ecore/OCL",
	invocationDelegates="http://www.eclipse.org/emf/2002/Ecore/OCL"
)
@GenModel(
	editDirectory="/work.andycarpenter.metamodel.waf.edit/src",
	editPluginID="work.andycarpenter.metamodel.waf.edit",
	childCreationExtenders="true",
	editorDirectory="/work.andycarpenter.metamodel.waf.editor/src",
	testsDirectory="/work.andycarpenter.metamodel.waf.tests/src",
	complianceLevel="17.0"
)
package work.andycarpenter.metamodel.waf

import org.eclipse.emf.common.util.BasicEList
import org.eclipse.emf.common.util.ECollections
import work.andycarpenter.metamodel.api.API
import work.andycarpenter.metamodel.base.Classifier
import work.andycarpenter.metamodel.base.DataType
import work.andycarpenter.metamodel.base.EnumerationType
import work.andycarpenter.metamodel.base.NamedDisplayElement
import work.andycarpenter.metamodel.base.NamedElement
import work.andycarpenter.metamodel.expression.Expression
import work.andycarpenter.metamodel.expression.Predicate
import work.andycarpenter.metamodel.orm.Association
import work.andycarpenter.metamodel.orm.AssociationPathElement
import work.andycarpenter.metamodel.orm.Attribute
import work.andycarpenter.metamodel.orm.AttributePathElement
import work.andycarpenter.metamodel.orm.DateDetails
import work.andycarpenter.metamodel.orm.Entity
import work.andycarpenter.metamodel.orm.Feature
import work.andycarpenter.metamodel.orm.Filter
import work.andycarpenter.metamodel.orm.Label
import work.andycarpenter.metamodel.orm.LocationFeature
import work.andycarpenter.metamodel.orm.ModelLabel
import work.andycarpenter.metamodel.orm.OrmModel
import work.andycarpenter.metamodel.orm.Path
import work.andycarpenter.metamodel.orm.Repository
import work.andycarpenter.metamodel.orm.ResourceFeature
import work.andycarpenter.metamodel.orm.ResourcePathElement
import work.andycarpenter.metamodel.orm.Selection
import work.andycarpenter.metamodel.security.AuthorisationExpression
import work.andycarpenter.metamodel.security.SecurityModel
import work.andycarpenter.metamodel.service.BusinessOperation
import work.andycarpenter.metamodel.service.Service
import work.andycarpenter.metamodel.service.ServiceModel

annotation "http://www.eclipse.org/emf/2002/Ecore/OCL" as OCL


enum FrameworkTechnologies {
	JSF
	CakePHP = 1
	CodeIgniter = 2
	Kohana = 3
	Laravel = 4
	Symfony = 5
}

class WafModel {
	contains SiteProperties siteProperties
	refers OrmModel[1] persistence
	refers SecurityModel security
	refers ServiceModel[1] business
	refers API api
	contains ImageManipulation[] imageManipulations
	contains Navigation navigation opposite partOf
	contains Controller[] controllers opposite partOf

	@GenModel(propertyCategory="Interface")
	String defaultCancelLabel = ''
	@GenModel(propertyCategory="Interface")
	String defaultCancelIcon = ''
	@GenModel(propertyCategory="Interface")
	String defaultClearLabel = ''
	@GenModel(propertyCategory="Interface")
	String defaultClearIcon = ''
	@GenModel(propertyCategory="Interface")
	String defaultResetLabel = ''
	@GenModel(propertyCategory="Interface")
	String defaultResetIcon = ''
	@GenModel(propertyCategory="Interface")
	String defaultSaveLabel = ''
	@GenModel(propertyCategory="Interface")
	String defaultSaveIcon = ''
	@GenModel(propertyCategory="Interface")
	String defaultSearchLabel = ''
	@GenModel(propertyCategory="Interface")
	String defaultSearchIcon = ''

	op boolean hasAPI() {
		api !== null
	}

	@OCL(derivation="controllers->collect(c | c.editUnits())->asOrderedSet()")
    @GenModel(propertyCategory="Debug")
	refers derived readonly unsettable transient volatile EditUnit[] editUnits

	op boolean hasEditUnits() {
		!editUnits.isEmpty()
	}

	@OCL(derivation="controllers->collect(c | c.formUnits())->asOrderedSet()")
    @GenModel(propertyCategory="Debug")
	refers derived readonly unsettable transient volatile SingletonUnit[] formUnits

	op boolean hasFormUnits() {
		!formUnits.isEmpty()
	}

	@OCL(derivation="controllers->collect(c | c.collectionUnitsWithFilters())->asOrderedSet()")
    @GenModel(propertyCategory="Debug")
	refers derived readonly unsettable transient volatile CollectionUnit[] collectionUnitsWithFilters

	@OCL(derivation="controllers->collect(c | c.imageUnits())->asOrderedSet()")
    @GenModel(propertyCategory="Debug")
	refers derived readonly unsettable transient volatile ImageUnit[] imageUnits

	op boolean hasImageUnits() {
		!imageUnits.isEmpty()
	}

	@OCL(derivation="controllers->collect(c | c.imageCardsUnits())->asOrderedSet()")
    @GenModel(propertyCategory="Debug")
	refers derived readonly unsettable transient volatile ImageCardsUnit[] imageCardsUnits

	op boolean hasImageCardsUnits() {
		!imageCardsUnits.isEmpty()
	}

	@OCL(derivation="controllers->collect(c | c.autocompleteFields)->asOrderedSet()")
    @GenModel(propertyCategory="Debug")
	refers derived readonly unsettable transient volatile UnitAssociation[] autocompleteFields
	op boolean hasAutocompleteFields() {
		!autocompleteFields.isEmpty()
	}

	@OCL(derivation="controllers->collect(c | c.formFields)->asOrderedSet()")
    @GenModel(propertyCategory="Debug")
	refers derived readonly unsettable transient volatile UnitField[] formFields
	op boolean hasFormFields() {
		!formFields.isEmpty()
	}

	op boolean hasFormAssociations() {
		formFields.exists[f | f.isAssociationField()]
	}

	op boolean hasResourceFields() {
		controllers.exists[c | c.hasResourceFields()]
	}

	@OCL(derivation="if self.security.oclIsUndefined() then false else security.isAuthenticated endif")
    @GenModel(propertyCategory="Debug")
	derived readonly unsettable transient volatile boolean isAuthenticated

	boolean useFormFloatingLabels = 'false'

	@GenModel(propertyCategory="Style")
	String defaultPageClass = ''
	contains CardsUnitStyles defaultCardsStyles
	contains CollectionUnitStyles defaultCollectionStyles
	contains DefaultUnitStyles defaultControlStyles
	contains DefaultUnitStyles defaultDetailsStyles
	contains EditUnitStyles defaultEditStyles
	contains ContentUnitStyles defaultStaticStyles
	contains TabularUnitStyles defaultTabularStyles
	contains DefaultUnitStyles defaultUnitStyles

   	op WafModel genModel() {
		this
	}

	op WafModel uiModel() {
		this
	}
}

class SiteProperties {
	String[1] siteName
	String[1] siteTitle
	String[1] locale
	String webmasterEmail
	String copyrightText
	String metaDescription
	String logoImage
	refers ImageManipulation logoFilters
	FrameworkTechnologies frameworkTechnology
	boolean useDbSessionStorage
	String textEditorURL
	String siteTemplate
	String securityFromEmail = ''
	String securityFromName = ''
	boolean staticUnitsEditable
}

class Navigation {
	refers WafModel[1] partOf opposite navigation
	contains Menu[] menus opposite definedBy
	
	String navbarClass = "navbar"
	op boolean hasNavbarClass() {
		navbarClass.trim() !== ''
	}

	AuthenticationMenuPlacement authenticationMenuPlacement
	String authenticationBarClass = "nav"
	op boolean hasAuthenticationBarClass() {
		authenticationBarClass.trim() !== ''
	}
}

enum AuthenticationMenuPlacement {
	Above = 0
	Below = 1
	Integrated = 2
}

class Menu extends NamedDisplayElement {
	refers Navigation[1] definedBy opposite menus
	contains MenuEntry[] entries opposite partOf
	boolean omitCaption
	String styleClass
	String captionClass
	String layoutClass
}

abstract class MenuEntry {
	container Menu[1] partOf opposite entries
}

class SubmenuEntry extends Menu, MenuEntry {
	@OCL(derivation="entries
		->select(e | e.oclIsTypeOf(ActionMenuEntry)).oclAsType(ActionMenuEntry)
		->exists(e | e.alwaysVisible = true)")
    @GenModel(propertyCategory="Debug")
	boolean alwaysVisible
}

class ActionMenuEntry extends MenuEntry, NamedDisplayElement {
	refers ContentUnit[1] action
	boolean alwaysVisible = "false"
}

class EditStaticTextMenuEntry extends MenuEntry, NamedDisplayElement {
}


class CardsUnitStyles extends CollectionStyles {
	@GenModel(propertyCategory="Style")
	String badgeClass = ''
	@GenModel(propertyCategory="Style")
	String cardClass = ''
	@GenModel(propertyCategory="Style")
	String cardBodyClass = ''
	@GenModel(propertyCategory="Style")
	String cardFooterClass = ''
	@GenModel(propertyCategory="Style")
	String cardHeaderClass = ''
}

abstract class ContentUnitStyles {
	@GenModel(propertyCategory="Style")
	String captionClass = ''
	@GenModel(propertyCategory="Style")
	String contentClass = ''
	@GenModel(propertyCategory="Style")
	String sectionClass = ''
}

abstract class CollectionStyles extends DynamicUnitStyles {
	@GenModel(propertyCategory="Style")
	String filtersClass = ''
	@GenModel(propertyCategory="Style")
	String filtersControlClass = ''
	@GenModel(propertyCategory="Style")
	String filtersActionClass = ''
	@GenModel(propertyCategory="Style")
	String instanceControlClass = ''
	@GenModel(propertyCategory="Style")
	String instanceActionClass = ''
}

class CollectionUnitStyles extends CollectionStyles {
}

class DefaultUnitStyles extends DynamicUnitStyles {
}

abstract class DynamicUnitStyles extends ContentUnitStyles {
	@GenModel(propertyCategory="Style")
	String actionClass = ''
	@GenModel(propertyCategory="Style")
	String controlClass = ''
	@GenModel(propertyCategory="Style")
	String fieldDefinitionListClass = ''
	@GenModel(propertyCategory="Style")
	String fieldLabelClass = ''
	@GenModel(propertyCategory="Style")
	String fieldListClass = ''
	@GenModel(propertyCategory="Style")
	String fieldValueClass = ''
}

class EditUnitStyles extends DynamicUnitStyles {
	@GenModel(propertyCategory="Style")
	String inputGroupClass = ''
}

class TabularUnitStyles extends CollectionStyles {
	@GenModel(propertyCategory="Style")
	String footerClass = ''
	@GenModel(propertyCategory="Style")
	String headerClass = ''
	@GenModel(propertyCategory="Style")
	String rowClasses = ''
}


abstract class DisplayElement {
	String[1] displayLabel = ''
}


class ImageManipulation extends NamedElement {
	int jpegQuality = "-1"
	contains ImageFilter[] filters
}


abstract class ImageFilter {
}

enum BackgroundPositions {
	TopLeft = 0,
	Top = 1,
	TopRight = 2,
	Left = 3,
	Center = 4,
	Right = 5,
	BottomLeft = 6,
	Bottom = 7,
	BottomRight = 8
}

class BackgroundFilter extends ImageFilter {
	String color
	int width
	int height
	BackgroundPositions position
}


enum ThumbnailModes {
	Crop = 0,
	Resize = 1
}

class ThumbnailFilter extends ImageFilter {
	int width
	int height
	ThumbnailModes mode
}


abstract class Controller extends NamedDisplayElement {
	container WafModel partOf opposite controllers

	refers Controller parentController opposite childControllers

	refers Controller[] childControllers opposite parentController

	contains ContentUnit[] units opposite controller

	op boolean hasUnits() {
		!units.isEmpty()
	}

	op boolean isSinglePage() {
		false
	}

	op boolean isMultiPage() {
		false
	}

	op AuthorisationExpression commonAuthorisation() {
		if (!hasUnits()) {
			null
		} else if (hasCommonUnitAuthorisation) {
			units.head.authorisation
		} else {
			null
		}
	}

	op boolean isAuthorised() {
		commonAuthorisation() !== null
	}

	op boolean hasAuthorisation() {
		isAuthorised() || units.exists[u | u.hasAuthorisation()]
	}

	op boolean hasCommonUnitAuthorisation() {
		if (units.forall[u | !u.isAuthorised()]) {
			true
		} else if (!units.forall[u | u.isAuthorised()]) {
			false
		} else if (units.size < 2) {
			true
		} else {
			units.drop(1).forall[u | u.authorisation.areSame(units.head.authorisation)]
		}
	}

	op boolean hasChangableCollections() {
		!editUnits().filter[it.hasChangableCollections].isEmpty()
	}

	op boolean hasCaptchaUnits() {
		!editUnits().filter[it.hasCaptcha].isEmpty()
	}

	op CollectionUnit[] collectionUnits() {
		units.filter[it instanceof CollectionUnit].map[it as CollectionUnit].asEList()
	}
	op boolean hasCollectionUnits() {
		!collectionUnits().isEmpty()
	}

	op DynamicUnit[] dynamicUnits() {
		units.filter[it instanceof DynamicUnit].map[it as DynamicUnit].toEList()
	}
	op boolean hasDynamicUnits() {
		!dynamicUnits().isEmpty()
	}

	op EditUnit[] editUnits() {
		dynamicUnits().filter[it instanceof EditUnit].map[it as EditUnit].toEList()
	}
	op boolean hasEditUnits() {
		!editUnits().isEmpty()
	}

	op SingletonUnit[] formUnits() {
		dynamicUnits().filter[it.isFormUnit].map[it as SingletonUnit].toEList()
	}
	op boolean hasFormUnits() {
		!formUnits().isEmpty()
	}

	op CollectionUnit[] collectionUnitsWithFilters() {
		collectionUnits().filter[it.hasCollectionFilters()].toEList()
	}

	op boolean hasCollectionFilters() {
		!collectionUnitsWithFilters().isEmpty()
	}

	op boolean hasNumericFilters() {
		collectionUnits().exists[u | u.hasNumericFilters]
	}

	op boolean hasTextFilters() {
		collectionUnits().exists[u | u.hasTextFilters]
	}

	op GalleryUnit[] galleryUnits() {
		dynamicUnits().filter[it instanceof GalleryUnit].map[it as GalleryUnit].toEList()
	}
	op boolean hasGalleryUnits() {
		!galleryUnits().isEmpty()
	}

	op TabularUnit[] tabularUnits() {
		dynamicUnits().filter[it instanceof TabularUnit].map[it as TabularUnit].toEList()
	}
	op boolean hasTabularUnits() {
		!tabularUnits().isEmpty()
	}

	op ImageUnit[] imageUnits() {
		dynamicUnits().filter[it instanceof ImageUnit].map[it as ImageUnit].toEList()
	}
	op boolean hasImageUnits() {
		!imageUnits().isEmpty()
	}

	op ImageCardsUnit[] imageCardsUnits() {
		imageUnits().filter[it instanceof ImageCardsUnit].map[it as ImageCardsUnit].toEList()
	}
	op boolean hasImageCardsUnits() {
		!imageCardsUnits().isEmpty()
	}

	op MapUnit[] mapUnits() {
		dynamicUnits().filter[it instanceof MapUnit].map[it as MapUnit].toEList()
	}
	op boolean hasMapUnits() {
		!mapUnits().isEmpty()
	}

	op SliderUnit[] sliderUnits() {
		dynamicUnits().filter[it instanceof SliderUnit].map[it as SliderUnit].toEList()
	}
	op boolean hasSliderUnits() {
		!sliderUnits().isEmpty()
	}

	@OCL(derivation="dynamicUnits()->collect(u | u.servicesUsed)->asOrderedSet()")
    @GenModel(propertyCategory="Debug")
	refers derived readonly unsettable transient volatile Service[] servicesUsed

	op boolean hasPagination() {
		collectionUnits().exists[u | u.hasPagination]
	}

	@OCL(derivation="dynamicUnits()->collect(u | u.associationFields())->asOrderedSet()")
    @GenModel(propertyCategory="Debug")
	refers derived readonly unsettable transient volatile UnitAssociation[] associationFields

	@OCL(derivation="dynamicUnits()->collect(u | u.autocompleteFields())->asOrderedSet()")
    @GenModel(propertyCategory="Debug")
	refers derived readonly unsettable transient volatile UnitAssociation[] autocompleteFields
	op boolean hasAutocompleteFields() {
		!autocompleteFields.isEmpty()
	}

	@OCL(derivation="dynamicUnits()->collect(u | u.formFields)->asOrderedSet()")
    @GenModel(propertyCategory="Debug")
	refers derived unsettable readonly transient volatile UnitField[] formFields

	op boolean hasImageFields() {
		!dynamicUnits().filter[it.hasImageFields].isEmpty
	}

	op boolean hasInterfaceFields() {
		!dynamicUnits().filter[it.hasInterfaceFields].isEmpty
	}

	op boolean hasRequiredInterfaceFields() {
		!dynamicUnits().filter[it.hasRequiredInterfaceFields].isEmpty
	}

	op boolean hasResourceFields() {
		!dynamicUnits().filter[it.hasResourceFields].isEmpty
	}

	@OCL(derivation="dynamicUnits()->collect(u | u.actionsWithoutTarget())->asOrderedSet()")
    @GenModel(propertyCategory="Debug")
	refers derived readonly unsettable transient volatile Action[] actionsWithoutTarget
	
	@OCL(derivation="dynamicUnits()->collect(u | u.fileActions())->asOrderedSet()")
    @GenModel(propertyCategory="Debug")
	refers derived readonly unsettable transient volatile OperationAction[] fileActions
	op boolean hasFileActions() {
		!fileActions.isEmpty()
	}

	op boolean hasActionsWithDisabledMessage() {
		!dynamicUnits().filter[it.hasActionsWithDisabledMessage()].isEmpty()
	}

	op boolean hasSuccessMessages() {
		!dynamicUnits().filter[it.hasActionsWithSuccessMessage()].isEmpty()
	}

	op boolean hasMessages() {
		!editUnits.filter[it.hasMessages].isEmpty()
		|| !dynamicUnits().filter[it.hasActionsWithMessages()].isEmpty()
	}

    @GenModel(propertyCategory="Navigation")
	String uriElement = ''

    @GenModel(propertyCategory="Style")
	String[1] localPageClass = ''
	op String pageClass() {
		if (localPageClass.trim() !== '') {
			localPageClass.trim()
		} else if (uiModel().defaultPageClass.trim() !== '') {
			uiModel().defaultPageClass.trim()
		} else {
			''
		}
	}
	op boolean hasPageClass() {
		pageClass() !== ''
	}

   	op WafModel genModel() {
		partOf.genModel()
	}

   	op WafModel uiModel() {
		partOf.uiModel()
	}

	@OCL(derivation="if parentController = null then
			self
		else
			parentController.rootController
		endif")
    @GenModel(propertyCategory="Debug")
	refers readonly transient volatile unsettable derived Controller rootController
}

class SinglePageController extends Controller {
	op boolean isSinglePage() {
		true
	}
}

class MultiPageController extends Controller {
	op boolean isMultiPage() {
		true
	}
}

abstract class ContentUnit extends NamedDisplayElement {
	container Controller[1] controller opposite units

    @GenModel(propertyCategory="Navigation")
	String uriElement = ''

    @GenModel(propertyCategory="Navigation")
    refers ContentUnit uriParent

	contains AuthorisationExpression authorisation
	op boolean isAuthorised() {
		authorisation !== null
	}

	op boolean hasAuthorisation() {
		isAuthorised()
	}

	String purposeSummary
	String alternative
	boolean omitCaption

	@OCL(derivation="self.oclIsKindOf(EditUnit) or self.oclIsKindOf(ControlUnit)")
    @GenModel(propertyCategory="Debug")
	derived readonly transient volatile boolean isFormUnit

	@GenModel(propertyCategory="Style")
	String captionClassOverride = ''
	@GenModel(propertyCategory="Style")
	String contentClassOverride = ''
	@GenModel(propertyCategory="Style")
	String sectionClassOverride = ''

	op String captionClass() {
		captionClassOverride.trim()
	}
	op boolean hasCaptionClass() {
		captionClass() !== ''
	}

	op String contentClass() {
		contentClassOverride.trim()
	}
	op boolean hasContentClass() {
		contentClass() !== ''
	}

	op String sectionClass() {
		sectionClassOverride.trim()
	}
	op boolean hasSectionClass() {
		sectionClass() !== ''
	}

	op WafModel genModel() {
		controller.genModel()
	}

	op WafModel uiModel() {
		controller.uiModel()
	}
}

class StaticUnit extends ContentUnit {
	@GenModel(propertyCategory="Business")
	refers Repository repository
	@GenModel(propertyCategory="Business")
	String[1] content

	op String captionClass() {
		if (captionClassOverride.trim() !== '') {
			contentClassOverride.trim()
		} else if (uiModel().defaultStaticStyles.captionClass.trim() !== '') {
			uiModel().defaultStaticStyles.captionClass.trim()
		} else if (uiModel().defaultUnitStyles.captionClass.trim() !== '') {
			uiModel().defaultUnitStyles.captionClass.trim()
		} else {
			''
		}
	}

	op String contentClass() {
		if (contentClassOverride.trim() !== '') {
			contentClassOverride.trim()
		} else if (uiModel().defaultStaticStyles.contentClass.trim() !== '') {
			uiModel().defaultStaticStyles.contentClass.trim()
		} else if (uiModel().defaultUnitStyles.contentClass.trim() !== '') {
			uiModel().defaultUnitStyles.contentClass.trim()
		} else {
			''
		}
	}

	op String sectionClass() {
		if (sectionClassOverride.trim() !== '') {
			sectionClassOverride.trim()
		} else if (uiModel().defaultStaticStyles.sectionClass.trim() !== '') {
			uiModel().defaultStaticStyles.sectionClass.trim()
		} else {
			uiModel().defaultUnitStyles.sectionClass.trim()
		}
	}
}

abstract class DynamicUnit extends ContentUnit, ActionContainer {
	refers Entity[1] contentType

	op boolean hasAuthorisation() {
		isAuthorised() || actions().exists[a | a.isAuthorised()]
	}

	@OCL(derivation="if not contentType.oclIsUndefined() then
			contentType.containingAssociation
		else
			null
		endif")
    @GenModel(propertyCategory="Debug")
	refers derived readonly unsettable transient volatile Association containingAssociation

	op Entity containingType() {
		if (contentType === null) {
			null
		} else {
			contentType.containingType
		}
	}

	op boolean isContained() {
		containingType() !== null
	}

	op Entity[] referencableTypes() {
		var types = new BasicEList<Entity>()
		if (contentType !== null) {
			types.add(contentType)
		}
		return types
	}

	@OCL(derivation="if self.oclIsKindOf(EditUnit) and not containingAssociation.oclIsUndefined() then
			featureFields()->exists(f | f.leafFeature() = containingAssociation)
		else
			false
		endif")
    @GenModel(propertyCategory="Debug")
	derived readonly unsettable transient volatile boolean hasExplicitlyEditedContainer

	@OCL(derivation="operationActions()->collect(u | u.servicesUsed)->asOrderedSet()")
    @GenModel(propertyCategory="Debug")
	refers readonly transient volatile unsettable derived Service[] servicesUsed
	@OCL(derivation="servicesUsed->notEmpty()")
    @GenModel(propertyCategory="Debug")
	derived readonly unsettable transient volatile boolean hasServicesUsed

    @GenModel(propertyCategory="Business")
	contains RouteActual[] routeActuals

    @GenModel(propertyCategory="Model")
	contains UnitField[] displayFields opposite displayedOn

    @GenModel(propertyCategory="Business")
	contains Predicate hideWhen

	op boolean isConditionallyDisplayed() {
		hideWhen !== null
	}

    @GenModel(propertyCategory="Interface")
	String messageWhenHidden = ''

	op boolean hasHiddenMessage() {
		isConditionallyDisplayed() && messageWhenHidden !== ''
	}

	String header
	String footer

	op UnitAssociation[] associationFields() {
		displayFields.filter[it.isAssociationField()].map[it as UnitAssociation].toEList()
	}

	op boolean hasAssociationFields() {
		!associationFields().isEmpty()
	}

	op UnitAssociation[] autocompleteFields() {
		if (this instanceof EditUnit) {
			associationFields.filter[it.useAutocomplete].toEList()
		} else {
			new BasicEList<UnitAssociation>()
		}
	}

	op boolean hasAutocompleteFields() {
		!autocompleteFields().isEmpty()
	}

	op boolean hasBooleanFields() {
		displayFields.exists[it.isBooleanField()]
	}

	op boolean hasCaptcha() {
		if (this instanceof EditUnit) {
			(this as EditUnit).useCaptcha
		} else {
			false
		}
	}

	op boolean hasChangableCollections() {
		if (this instanceof EditUnit) {
			collectionFields()
				.filter[it.isFeatureField()].map[it as UnitFeature]
				.exists[it.collectionUiAllowAdd || it.collectionUiAllowRemove]
		} else {
			false
		}
	}

 	op UnitField[] collectionFields() {
		displayFields.filter[!it.isSingletonField()].toEList()
	}

 	op UnitFeature[] collectionFeatures() {
		collectionFields.filter[it.isFeatureField()].map[it as UnitFeature].toEList()
	}

 	op UnitField[] dataTypeFields() {
		displayFields.filter[it.isDataTypeField()].toEList()
	}

 	op UnitField[] dateFields() {
		displayFields.filter[it.isDateField()].toEList()
	}

 	op UnitField[] defaultValueFields() {
		displayFields.filter[it.hasDefaultValue()].toEList()
	}

	op boolean hasDefaultValueFields() {
		!defaultValueFields().isEmpty()
	}

	op boolean hasDisabledFields() {
		displayFields.exists[it.isDisabled]
	}

	op boolean hasEmailFields() {
		displayFields.exists[it.isEmailField()]
	}

 	op UnitField[] encryptedFields() {
		displayFields.filter[it.isEncryptedField()].toEList()
	}

	op boolean hasEncryptedFields() {
		!encryptedFields().isEmpty()
	}

 	op UnitField[] enumerationFields() {
		displayFields.filter[it.isEnumerationField()].toEList()
	}

	op boolean hasEnumerationFields() {
		!enumerationFields().isEmpty()
	}

 	op UnitFeature[] featureFields() {
		displayFields.filter[it.isFeatureField()].map[it as UnitFeature].toEList()
	}

	op boolean hasFeatureFields() {
		!featureFields().isEmpty()
	}

 	op UnitResource[] fileFields() {
		resourceFields.filter[it.resource.isFileFeature].toEList()
	}

	op boolean hasFileFields() {
		!fileFields().isEmpty()
	}

 	op UnitFeature[] forcedValueFields() {
		displayFields.filter[it.hasForcedValue()].map[it as UnitFeature].toEList()
	}

	op boolean hasForcedValueFields() {
		!forcedValueFields().isEmpty()
	}

	@OCL(derivation="if self.isFormUnit = true then
				displayFields
			else
				OrderedSet{}
			endif")
    @GenModel(propertyCategory="Debug")
	refers readonly transient volatile unsettable derived UnitField[] formFields

 	op UnitResource[] imageFields() {
		resourceFields.filter[it.resource.isImageFeature].toEList()
	}

	op boolean hasImageFields() {
		!imageFields().isEmpty()
	}

	op boolean hasIntegerFields() {
		displayFields.exists[it.isIntegerField()]
	}

 	op InterfaceField[] interfaceFields() {
		displayFields.filter[it instanceof InterfaceField].map[it as InterfaceField].toEList()
	}

	op boolean hasInterfaceFields() {
		!interfaceFields().isEmpty()
	}

	op boolean hasRequiredInterfaceFields() {
		interfaceFields.exists[it.isRequiredField()]
	}

	op boolean hasObfuscatedFields() {
		interfaceFields.exists[it.isObfuscatedField()]
	}

	op boolean hasRepeatedFields() {
		displayFields.exists[it.isRepeated]
	}

 	op UnitResource[] resourceFields() {
		displayFields.filter[it.isResourceField()].map[it as UnitResource].toEList()
	}

	op boolean hasResourceFields() {
		!resourceFields().isEmpty()
	}

	op boolean hasStringFields() {
		displayFields.exists[it.isStringField()]
	}

	op boolean hasTextareaFields() {
		displayFields.exists[it.isTextareaField()]
	}

	op boolean hasUrlFields() {
		displayFields.exists[it.isUrlField()]
	}

	@GenModel(propertyCategory="Style")
	String actionClassOverride = ''
	@GenModel(propertyCategory="Style")
	String controlClassOverride = ''
	@GenModel(propertyCategory="Style")
	String fieldDefinitionListClassOverride = ''
	@GenModel(propertyCategory="Style")
	String fieldLabelClassOverride = ''
	@GenModel(propertyCategory="Style")
	String fieldListClassOverride = ''
	@GenModel(propertyCategory="Style")
	String fieldValueClassOverride = ''

	op String actionClass() {
		actionClassOverride.trim()
	}
	op boolean hasActionClass() {
		actionClass() !== ''
	}

	op String controlClass() {
		controlClassOverride.trim()
	}
	op boolean hasControlClass() {
		controlClass() !== ''
	}

	op String fieldDefinitionListClass() {
		fieldDefinitionListClassOverride.trim()
	}
	op boolean hasFieldDefinitionListClass() {
		fieldDefinitionListClass() !== ''
	}

	op String fieldLabelClass() {
		fieldLabelClassOverride.trim()
	}
	op boolean hasFieldLabelClass() {
		fieldLabelClass() !== ''
	}

	op String fieldListClass() {
		fieldListClassOverride.trim()
	}
 	op boolean hasFieldListClass() {
		fieldListClass() !== ''
	}

	op String fieldValueClass() {
		fieldValueClassOverride.trim()
	}
	op boolean hasFieldValueClass() {
		fieldValueClass() !== ''
	}
}

class RouteActual {
	refers Attribute[1] routeParameter
	contains Expression[1] value
}

enum CollectionDisplayOptions {
	LineDirection
	PageDirection = 1
}

interface UnitField extends DisplayElement {
	container DynamicUnit[1] displayedOn opposite displayFields

  	op String fieldName() {
		''
	}

	contains AuthorisationExpression authorisation
	@OCL(derivation="not authorisation.oclIsUndefined()")
    @GenModel(propertyCategory="Debug")
	derived readonly unsettable transient volatile boolean isAuthorised

	@OCL(derivation="if self.isFeatureField() = true then
			(displayLabel.trim() <> '')
				and (displayLabel.trim() <> self.oclAsType(UnitFeature).leafFeature().displayLabel.trim())
		else if self.isInterfaceField() = true then
			true
		else
			false
		endif endif")
    @GenModel(propertyCategory="Debug")
	derived readonly unsettable transient volatile boolean hasCustomisedDisplayLabel

	@OCL(derivation="if self.isInterfaceField() = true then
			self.oclAsType(InterfaceField).repeatedField
		else
			false
		endif")
    @GenModel(propertyCategory="Debug")
	derived readonly unsettable transient volatile boolean isRepeated

	@OCL(derivation="if self.isFeatureField() = true then
			let feature : UnitFeature = self.oclAsType(UnitFeature)
				in if feature.titleOverride.trim() <> '' then
						feature.titleOverride.trim()
					else if feature.displayLabel.trim() <> '' then
						feature.displayLabel.trim()
					else if feature.leafFeature().displayLabel.trim() <> '' then
						feature.leafFeature().displayLabel.trim()
					else
						feature.leafFeature().name
					endif endif endif
		else if self.isInterfaceField() = true then
			let field : InterfaceField = self.oclAsType(InterfaceField)
				in if field.fieldTitle.trim() <> '' then
						field.fieldTitle.trim()
					else if field.displayLabel.trim() <> '' then
						field.displayLabel.trim()
					else
						field.name
					endif endif
		else
			''
		endif endif")
    @GenModel(propertyCategory="Debug")
	derived readonly unsettable transient volatile String title
	@OCL(derivation="title <> ''")
    @GenModel(propertyCategory="Debug")
	derived readonly unsettable transient volatile boolean hasTitle

	@OCL(derivation="if self.isFeatureField() = true then
			title <> self.oclAsType(UnitFeature).leafFeature().displayLabel.trim()
		else
			title <> ''
		endif")
    @GenModel(propertyCategory="Debug")
	derived readonly unsettable transient volatile boolean hasCustomisedTitle

	@OCL(derivation="if self.isInterfaceField() and self.isRepeated then
			self.oclAsType(InterfaceField).secondTitle.trim() <> ''
		else
			false
		endif")
    @GenModel(propertyCategory="Debug")
	derived readonly unsettable transient volatile boolean hasSecondTitle

	@OCL(derivation="if self.isFeatureField() = true then
			let feature : UnitFeature = self.oclAsType(UnitFeature)
				in if feature.placeholderOverride.trim() <> '' then
						feature.placeholderOverride.trim()
					else if feature.displayLabel.trim() <> '' then
						feature.displayLabel.trim()
					else if feature.leafFeature().displayLabel.trim() <> '' then
						feature.leafFeature().displayLabel.trim()
					else
						feature.leafFeature().name
					endif endif endif
		else if self.isInterfaceField() = true then
			let field : InterfaceField = self.oclAsType(InterfaceField)
				in if field.fieldPlaceholder.trim() <> '' then
						field.fieldPlaceholder.trim()
					else if field.displayLabel.trim() <> '' then
						field.displayLabel.trim()
					else
						field.name
					endif endif
		else
			''
		endif endif")
    @GenModel(propertyCategory="Debug")
	derived readonly unsettable transient volatile String placeholder
	@OCL(derivation="placeholder <> ''")
    @GenModel(propertyCategory="Debug")
	derived readonly unsettable transient volatile boolean hasPlaceholder

	@OCL(derivation="if self.isFeatureField() = true then
			placeholder <> self.oclAsType(UnitFeature).leafFeature().displayLabel.trim()
		else
			placeholder <> ''
		endif")
    @GenModel(propertyCategory="Debug")
	derived readonly unsettable transient volatile boolean hasCustomisedPlaceholder

	@OCL(derivation="if self.isInterfaceField() and self.isRepeated then
			self.oclAsType(InterfaceField).secondPlaceholder.trim() <> ''
		else
			false
		endif")
    @GenModel(propertyCategory="Debug")
	derived readonly unsettable transient volatile boolean hasSecondPlaceholder

	String localHelp = ''
	@OCL(derivation="if self.oclIsTypeOf(UnitLabel) then
			''
		else
			if localHelp.trim() <> '' then
				localHelp.trim()
			else
				''
			endif
		endif")
    @GenModel(propertyCategory="Debug")
	derived readonly unsettable transient volatile String help
	@OCL(derivation="help <> ''")
    @GenModel(propertyCategory="Debug")
	derived readonly unsettable transient volatile boolean hasHelp

	@OCL(derivation="if self.oclIsKindOf(InterfaceField) and self.isRepeated then
			self.oclAsType(InterfaceField).secondHelp.trim() <> ''
		else
			false
		endif")
    @GenModel(propertyCategory="Debug")
	derived readonly unsettable transient volatile boolean hasSecondHelp

	contains Predicate hideWhen

	String defaultDisplay
	contains Predicate displayDefaultWhen

  	op Action[] actions() {
		ECollections.emptyEList()
	}

 	op boolean isContains() {
		false
	}

 	op Classifier dataType() {
		null
	}

  	op DateDetails dateDetails() {
		null
	}

  	op Expression defaultValue() {
		null
	}

 	op boolean hasDefaultValue() {
		defaultValue() !== null
	}

 	op boolean hasDefaultDisplay() {
		defaultDisplay !== null
	}

 	op boolean isDisabled() {
		disabledInput
	}

 	op boolean hasDisplayDefaultWhen() {
		hasDefaultDisplay() && displayDefaultWhen !== null
	}

 	op boolean hasEmptyCollectionDisplay() {
		false
	}

  	op EnumerationType enumerationType() {
		null
	}

  	op Expression forcedValue() {
		null
	}

 	op boolean hasForcedValue() {
		forcedValue() !== null
	}

 	op boolean isAssociationField() {
		false
	}

 	op boolean isAttributeField() {
		false
	}

  	op boolean isBooleanField() {
		false
	}

  	op boolean isDataTypeField() {
		false
	}

  	op boolean isDateField() {
		false
	}

   	op boolean isEmailField() {
		false
	}

  	op boolean isEncryptedField() {
		false
	}

  	op boolean isEnumerationField() {
		false
	}

  	op boolean isFeatureField() {
		false
	}

  	op boolean isInterfaceField() {
		false
	}

  	op boolean isIntegerField() {
		false
	}

  	op boolean isLabelField() {
		false
	}

  	op boolean isLocationField() {
		false
	}

  	op boolean isObfuscatedField() {
		false
	}

 	op boolean isResourceField() {
		false
	}

   	op boolean isRequiredField() {
		false
	}

 	op boolean isSingletonField() {
		true
	}

 	op boolean isStringField() {
		false
	}

  	op boolean isTextareaField() {
		false
	}

  	op boolean isUrlField() {
		false
	}

	boolean escapeHTML = "true"
	CollectionDisplayOptions collectionDisplayOption
	boolean truncate
	String dateFormat
	boolean disabledInput
	String headerClass

	@GenModel(propertyCategory="Style")
	String localInputGroupClass = ''
	@GenModel(propertyCategory="Style")
	String localFieldLabelClass = ''
	@GenModel(propertyCategory="Style")
	String localFieldValueClass = ''

	op String inputGroupClass() {
		if (localInputGroupClass.trim() !== '') {
			localInputGroupClass.trim()
		}  else if (displayedOn instanceof EditUnit) {
			if ((displayedOn as EditUnit).hasInputGroupClass()) {
				(displayedOn as EditUnit).inputGroupClass()
			} else {
				''
			}
		}  else {
			''
		}
	}
	op boolean hasInputGroupClass() {
		inputGroupClass() !== ''
	}

	op String fieldLabelClass() {
		if (localFieldLabelClass.trim() !== '') {
			localFieldLabelClass.trim()
		}  else if (displayedOn.hasFieldLabelClass()) {
			displayedOn.fieldLabelClass()
		}  else {
			''
		}
	}
	op boolean hasFieldLabelClass() {
		fieldLabelClass() !== ''
	}

	op String fieldValueClass() {
		if (localFieldValueClass.trim() !== '') {
			localFieldValueClass.trim()
		}  else if (displayedOn.hasFieldValueClass()) {
			displayedOn.fieldValueClass()
		}  else {
			''
		}
	}
	op boolean hasFieldValueClass() {
		fieldLabelClass() !== ''
	}

   	op WafModel genModel() {
		displayedOn.genModel()
	}

   	op WafModel uiModel() {
		displayedOn.uiModel()
	}
}

abstract class UnitFeature extends UnitField, Path, ActionContainer {
  	op String fieldName() {
		name()
	}

	String titleOverride = ''
	String placeholderOverride = ''
	contains Expression featureForcedValue
	boolean collectionUiAllowAdd = "true"
	boolean collectionUiAllowRemove = "true"
	int collectionMaximumValues = "0"
	String emptyCollectionDisplay
	boolean autofocus
	String footer

	op Entity contextEntity() {
		displayedOn.contentType
	}

 	op boolean hasEmptyCollectionDisplay() {
		emptyCollectionDisplay !== null
	}

  	op Expression forcedValue() {
		featureForcedValue
	}

  	op boolean isAssociationField() {
		if (leafFeature() === null) {
			false
		} else {
			leafFeature() instanceof Association
		}
	}

 	op boolean isAttributeField() {
		if (leafFeature() === null) {
			false
		} else {
			leafFeature() instanceof Attribute
		}
	}

  	op boolean isBooleanField() {
		if (isDataTypeField()) {
			leafFeature().isBooleanFeature
		} else {
			false
		}
	}

  	op boolean isDataTypeField() {
		if (leafFeature() === null) {
			false
		} else {
			leafFeature().isDataTypeFeature
		}
	}

  	op boolean isDateField() {
		if (leafFeature() === null) {
			false
		} else {
			leafFeature().isDateFeature
		}
	}

  	op boolean isEmailField() {
		if (isDataTypeField()) {
			leafFeature().isEmailFeature
		} else {
			false
		}
	}

  	op boolean isEncryptedField() {
		if (isDataTypeField()) {
			false
		} else {
			leafFeature().isEncrypted
		}
	}

  	op boolean isEnumerationField() {
		if (isDataTypeField()) {
			leafFeature().isEnumerationFeature
		} else {
			false
		}
	}

   	op boolean isFeatureField() {
		true
	}

  	op boolean isIntegerField() {
		if (isDataTypeField()) {
			leafFeature().isIntegerFeature
		} else {
			false
		}
	}

  	op boolean isLocationField() {
		if (leafFeature() === null) {
			false
		} else {
			leafFeature().isLocationFeature
		}
	}

  	op boolean isObfuscatedField() {
		if (isDataTypeField()) {
			leafFeature().isObfuscatedFeature
		} else {
			false
		}
	}

   	op boolean isRequiredField() {
		if (leafFeature() === null) {
			true
		} else {
			leafFeature().isRequired
		}
	}

 	op boolean isResourceField() {
		if (leafFeature() === null) {
			false
		} else {
			leafFeature() instanceof ResourceFeature
		}
	}

   	op boolean isSingletonField() {
		if (leafFeature() === null) {
			true
		} else {
			leafFeature().isSingleton
		}
	}

  	op boolean isStringField() {
		if (isDataTypeField()) {
			leafFeature().isStringFeature
		} else {
			false
		}
	}

  	op boolean isTextareaField() {
		if (isDataTypeField()) {
			leafFeature().isTextareaFeature
		} else {
			false
		}
	}

  	op boolean isUrlField() {
		if (isDataTypeField()) {
			leafFeature().isUrlFeature
		} else {
			false
		}
	}

  	op Classifier dataType() {
		if (isDataTypeField()) {
			leafFeature().dataType
		} else {
			null
		}
	}

  	op DateDetails dateDetails() {
		if (isDateField()) {
			leafFeature().dateDetails
		} else {
			null
		}
	}

  	op EnumerationType enumerationType() {
		if (isEnumerationField()) {
			leafFeature().enumerationType
		} else {
			null
		}
	}
}

class UnitAssociation extends UnitFeature, AssociationPathElement {
	refers Selection options
	refers Attribute collectionSortBy
	boolean useAutocomplete = "false"

 	op boolean isContains() {
		if (isAssociationField()) {
			(leafFeature() as Association).isContains
		}  else {
			false
		}
	}
}

class UnitAttribute extends UnitFeature, AttributePathElement {
	contains Expression attributeDefaultValue
	boolean obfuscateFormFields
	String validationPattern = ''

  	op Expression defaultValue() {
		attributeDefaultValue
	}
}

class UnitResource extends UnitFeature, ResourcePathElement {
	boolean isDeletable = "true"
	boolean isDownloadable = "true"
	boolean showImageWhenEditing = "true"
	refers ImageManipulation imageFilter
}

class UnitLabel extends UnitField {
	refers ModelLabel[1] label

  	op String name() {
		if (label === null) {
			''
		} else {
			label.name
		}
	}

  	op String fieldName() {
		name()
	}

  	op boolean isLabelField() {
		true
	}
}

abstract class InterfaceField extends NamedElement, UnitField {
  	op String name() {
		getName()
	}

  	op String fieldName() {
		name()
	}

	String fieldTitle = ''
	String fieldPlaceholder = ''
	boolean fieldRequired
	contains Expression fieldDefaultValue
	String interfaceType
	String validationPattern = ''
	boolean repeatedField = 'false'
	String secondDisplayLabel = ''
	String secondTitle = ''
	String secondPlaceholder = ''
	String secondHelp = ''

  	op Expression defaultValue() {
		fieldDefaultValue
	}

   	op boolean isInterfaceField() {
		true
	}

   	op boolean isRequiredField() {
		fieldRequired
	}
}

class DataTypeField extends InterfaceField {
	refers DataType[1] interfaceDataType
	boolean caseInsensitive
	boolean obfuscateFormFields
	boolean encrypt

  	op boolean isBooleanField() {
		if (interfaceDataType === null) {
			false
		} else {
			interfaceDataType.isBooleanDataType
		}
	}

  	op boolean isDataTypeField() {
		true
	}

  	op boolean isEmailField() {
		if (interfaceDataType === null) {
			interfaceDataType.isEmailDataType
		} else {
			false
		}
	}
	
  	op boolean isEncryptedField() {
		encrypt
	}

  	op boolean isEnumerationField() {
		if (interfaceDataType === null) {
			false
		} else {
			interfaceDataType.isEnumerationType
		}
	}

  	op boolean isIntegerField() {
		if (interfaceDataType === null) {
			false
		} else {
			interfaceDataType.isIntegerDataType
		}
	}

  	op boolean isObfuscatedField() {
		obfuscateFormFields
	}

  	op boolean isStringField() {
		if (interfaceDataType === null) {
			false
		} else {
			interfaceDataType.isStringDataType
		}
	}

  	op boolean isTextareaField() {
		if (interfaceDataType === null) {
			false
		} else {
			interfaceDataType.isTextareaDataType
		}
	}

  	op boolean isUrlField() {
		if (interfaceDataType === null) {
			false
		} else {
			interfaceDataType.isUrlDataType
		}
	}
}

class DateField extends InterfaceField {
	DateDetails[1] details
	String format

  	op boolean isDateField() {
		true
	}
}

interface SelectableUnit {
	@GenModel(propertyCategory="Business")
	refers SelectAction[] selectors opposite target
}

class SingletonUnit extends DynamicUnit {
	refers Label title
}

abstract class CollectionUnit extends DynamicUnit, SelectableUnit, ValueContext {
	@GenModel(propertyCategory="Interface")
	refers Label unitTitle
	contains DisplayValue elementTitle
	@GenModel(propertyCategory="Interface")
	boolean truncateElementTitle = "false"

	@GenModel(propertyCategory="Business")
	refers Selection selection
	@GenModel(propertyCategory="Business")
	refers Filter filter
	@GenModel(propertyCategory="Business")
	refers Selection findContainerSelection
	@GenModel(propertyCategory="Business")
	refers Selection findElementSelection

	op Entity containingType() {
		if (findContainerSelection !== null) {
			findContainerSelection.definedBy.serves
		} else if (selection !== null) {
			if (selection.hasSelectPath()) {
				selection.selectionType()
			} else {
				null
			} 
		} else if (contentType !== null) {
			contentType.containingType
		} else {
			null
		}
	}

	op Entity[] referencableTypes() {
		var entities = new BasicEList<Entity>()
		if (contentType !== null) {
			entities.add(contentType);
			if (isContained()) {
				entities.add(containingType());
			}
		}
		return entities
	}

	@GenModel(propertyCategory="Pagination")
	int defaultPaginationSize = '0'
	@GenModel(propertyCategory="Pagination")
	int maximumPaginationSize

	op boolean hasPagination() {
		defaultPaginationSize > 0
		&& if (selection !== null) {
				selection.hasPaginationSupport
			} else {
				true
			} 
	}

	op boolean hasGroupedResults() {
		if (selection !== null) {
				!selection.grouping.isEmpty()
			} else {
				false
			} 
	}

	contains CollectionFilter[] filters opposite affects

	op boolean hasCollectionFilters() {
		!filters.isEmpty()
	}

	op boolean hasNumericFilters() {
		!filters.filter[it instanceof NumericFilter].isEmpty()
	}

	op boolean hasTextFilters() {
		!filters.filter[it instanceof TextFilter].isEmpty()
	}

	@GenModel(propertyCategory="Interface")
	String clearLabelOverride = ''
	@GenModel(propertyCategory="Interface")
	String clearIconOverride = ''
	@GenModel(propertyCategory="Interface")
	String searchLabelOverride = ''
	@GenModel(propertyCategory="Interface")
	String searchIconOverride = ''

	op boolean hasCustomClearLabel() {
		clearLabelOverride.trim() !== ''
		&& clearLabelOverride.trim() !== controller.partOf.defaultClearLabel.trim()
	}
	op String clearLabel() {
		if (hasCustomClearLabel()) {
			clearLabelOverride.trim()
		} else if (controller.partOf.defaultClearLabel.trim() !== '') {
			controller.partOf.defaultClearLabel.trim()
		} else {
			'Clear'
		} 
	}

	op boolean hasCustomClearIcon() {
		clearIconOverride.trim() !== ''
		&& clearIconOverride.trim() !== controller.partOf.defaultClearIcon.trim()
	}
	op String clearIcon() {
		if (hasCustomClearIcon()) {
			clearIconOverride.trim()
		} else {
			controller.partOf.defaultClearIcon.trim()
		} 
	}
	op boolean hasClearIcon() {
		clearIconOverride.trim() !== ''
		|| controller.partOf.defaultClearIcon.trim() !== ''
	}

	op boolean hasCustomSearchLabel() {
		searchLabelOverride.trim() !== ''
		&& searchLabelOverride.trim() !== controller.partOf.defaultSearchLabel.trim()
	}
	op String searchLabel() {
		if (hasCustomSearchLabel()) {
			searchLabelOverride.trim()
		} else if (controller.partOf.defaultSearchLabel.trim() !== '') {
			controller.partOf.defaultSearchLabel.trim()
		} else {
			'Search'
		} 
	}

	op boolean hasCustomSearchIcon() {
		searchIconOverride.trim() !== ''
		&& searchIconOverride.trim() !== controller.partOf.defaultSearchIcon.trim()
	}
	op String searchIcon() {
		if (hasCustomSearchIcon()) {
			searchIconOverride.trim()
		} else {
			controller.partOf.defaultSearchIcon.trim()
		} 
	}
	op boolean hasSearchIcon() {
		searchIconOverride.trim() !== ''
		|| controller.partOf.defaultSearchIcon.trim() !== ''
	}

	@GenModel(propertyCategory="Interface")
	String emptyMessage = ''
	@GenModel(propertyCategory="Interface")
	boolean omitFieldLabels

	@GenModel(propertyCategory="Style")
	String[1] filtersClassOverride = ''
	@GenModel(propertyCategory="Style")
	String[1] filtersControlClassOverride = ''
	@GenModel(propertyCategory="Style")
	String[1] filtersActionClassOverride = ''
	@GenModel(propertyCategory="Style")
	String[1] instanceControlClassOverride = ''
	@GenModel(propertyCategory="Style")
	String[1] instanceActionClassOverride = ''

	op String filtersClass() {
		filtersClassOverride.trim()
	}
	op boolean hasFiltersClass() {
		filtersClass() !== ''
	}

	op String filtersControlClass() {
		filtersControlClassOverride.trim()
	}
	op boolean hasFiltersControlClass() {
		filtersControlClass() !== ''
	}

	op String filtersActionClass() {
		filtersActionClassOverride.trim()
	}
	op boolean hasFiltersActionClass() {
		filtersActionClass() !== ''
	}

	op String instanceControlClass() {
		instanceControlClassOverride.trim()
	}
	op boolean hasInstanceControlClass() {
		instanceControlClass() !== ''
	}

	op String instanceActionClass() {
		instanceActionClassOverride.trim()
	}
	op boolean hasInstanceActionClass() {
		instanceActionClass() !== ''
	}
}

abstract class CollectionFilter extends NamedElement {
	container CollectionUnit[1] affects opposite filters
    @GenModel(propertyCategory="Model")
	refers Attribute[1] attribute
    @GenModel(propertyCategory="Interface")
	String help = ''
    @GenModel(propertyCategory="Interface")
	String placeholderOverride = ''
    @GenModel(propertyCategory="Interface")
	String titleOverride = ''

	op Entity[] referencableTypes() {
		var entities = new BasicEList<Entity>()
		if (affects.selection !== null) {
			entities.addAll(affects.selection.referencableTypes());
		} else {
			entities.add(affects.contentType);
		}

		return entities
	}

	op boolean hasHelp() {
		help.trim() !== ''
	}

	op boolean hasCustomisedPlaceholder() {
		if (placeholderOverride.trim() === '' || attribute === null) {
			false
		} else {
			placeholderOverride.trim() !== attribute.displayLabel.trim()
		}
	}
	op String placeholder() {
		if (hasCustomisedPlaceholder()) {
			placeholderOverride.trim()
		} else {
			if (attribute !== null) {
				attribute.displayLabel.trim()
			} else {
				''
			}
		}
	}

	op boolean hasCustomisedTitle() {
		if (titleOverride.trim() === '' || attribute === null) {
			false
		} else {
			titleOverride.trim() !== attribute.displayLabel.trim()
		}
	}
	op String title() {
		if (hasCustomisedTitle()) {
			titleOverride.trim()
		} else {
			if (attribute !== null) {
				attribute.displayLabel.trim()
			} else {
				''
			}
		}
	}
}

class ArrayFilter extends CollectionFilter {
}

class BooleanFilter extends CollectionFilter {
	refers Feature feature
}

class ChoiceFilter extends CollectionFilter {
}

class ComparisonFilter extends CollectionFilter {
}

class DateTimeFilter extends CollectionFilter {
}

class EntityFilter extends CollectionFilter {
}

class NullFilter extends CollectionFilter {
}

class NumericFilter extends CollectionFilter {
}

class TextFilter extends CollectionFilter {
}

abstract class EditUnit extends SingletonUnit {
	@GenModel(propertyCategory="Business")
	boolean useCaptcha = 'false'
	@GenModel(propertyCategory="Model")
	boolean onSaveContinueEditing = 'false'
	contains Predicate disableWhen

	@GenModel(propertyCategory="Model")
	refers ContentUnit confirmDestination
	@GenModel(propertyCategory="Model")
	refers ContentUnit cancelDestination

	@GenModel(propertyCategory="Interface")
	String cancelLabelOverride = ''
	@GenModel(propertyCategory="Interface")
	String cancelIconOverride = ''
	@GenModel(propertyCategory="Interface")
	String resetLabelOverride = ''
	@GenModel(propertyCategory="Interface")
	String resetIconOverride = ''
	@GenModel(propertyCategory="Interface")
	String[1] saveLabelOverride = ''
	@GenModel(propertyCategory="Interface")
	String saveIconOverride = ''

	op boolean hasCustomCancelLabel() {
		cancelLabelOverride.trim() !== ''
		&& cancelLabelOverride.trim() !== controller.partOf.defaultCancelLabel.trim()
	}
	op String cancelLabel() {
		if (hasCustomCancelLabel()) {
			cancelLabelOverride.trim()
		} else {
			controller.partOf.defaultCancelLabel.trim()
		} 
	}

	op boolean hasCustomCancelIcon() {
		cancelIconOverride.trim() !== ''
		&& cancelIconOverride.trim() !== controller.partOf.defaultCancelIcon.trim()
	}
	op String cancelIcon() {
		if (hasCustomCancelIcon()) {
			cancelIconOverride.trim()
		} else {
			controller.partOf.defaultCancelIcon.trim()
		} 
	}
	op boolean hasCancelIcon() {
		cancelIconOverride.trim() !== ''
		|| controller.partOf.defaultCancelIcon.trim() !== ''
	}

	op boolean hasCustomResetLabel() {
		resetLabelOverride.trim() !== ''
		&& resetLabelOverride.trim() !== controller.partOf.defaultResetLabel.trim()
	}
	op String resetLabel() {
		if (hasCustomResetLabel()) {
			resetLabelOverride.trim()
		} else {
			controller.partOf.defaultResetLabel.trim()
		} 
	}

	op boolean hasCustomResetIcon() {
		resetIconOverride.trim() !== ''
		&& resetIconOverride.trim() !== controller.partOf.defaultResetIcon.trim()
	}
	op String resetIcon() {
		if (hasCustomResetIcon()) {
			resetIconOverride.trim()
		} else {
			controller.partOf.defaultResetIcon.trim()
		} 
	}
	op boolean hasResetIcon() {
		resetIconOverride.trim() !== ''
		|| controller.partOf.defaultResetIcon.trim() !== ''
	}

	op boolean hasCustomSaveLabel() {
		saveLabelOverride.trim() !== ''
		&& saveLabelOverride.trim() !== controller.partOf.defaultSaveLabel.trim()
	}
	op String saveLabel() {
		if (hasCustomSaveLabel()) {
			saveLabelOverride.trim()
		} else if (controller.partOf.defaultSaveLabel.trim() !== '') {
			controller.partOf.defaultSaveLabel.trim()
		} else {
			if (this instanceof CreateUnit) {
				'Create'
			} else if (this instanceof CreateUpdateUnit) {
				'Save'
			} else if (this instanceof UpdateUnit) {
				'Update'
			} else if (this instanceof MapUnit) {
				'Save'
			} else {
				'Edit'
			} 
		} 
	}

	op boolean hasCustomSaveIcon() {
		saveIconOverride.trim() !== ''
		&& saveIconOverride.trim() !== controller.partOf.defaultSaveIcon.trim()
	}
	op String saveIcon() {
		if (hasCustomSaveIcon()) {
			saveIconOverride.trim()
		} else {
			controller.partOf.defaultSaveIcon.trim()
		} 
	}
	op boolean hasSaveIcon() {
		saveIconOverride.trim() !== ''
		|| controller.partOf.defaultSaveIcon.trim() !== ''
	}


	@GenModel(propertyCategory="Interface")
	contains Message confirmMessage
	@GenModel(propertyCategory="Interface")
	contains Message successMessage

	op boolean hasConfirmMessage() {
		confirmMessage !== null
	}

	op boolean hasSuccessMessage() {
		successMessage !== null
	}

	op boolean hasMessages() {
		hasConfirmMessage() || hasSuccessMessage()
	}

	@GenModel(propertyCategory="Style")
	String[1] inputGroupClassOverride = ''

	op String actionClass() {
		if (actionClassOverride.trim() !== '') {
			actionClassOverride.trim()
		} else if (uiModel().defaultEditStyles.actionClass.trim() !== '') {
			uiModel().defaultEditStyles.actionClass.trim()
		} else {
			uiModel().defaultUnitStyles.actionClass.trim()
		}
	}

	op String captionClass() {
		if (captionClassOverride.trim() !== '') {
			contentClassOverride.trim()
		} else if (uiModel().defaultEditStyles.captionClass.trim() !== '') {
			uiModel().defaultEditStyles.captionClass.trim()
		} else {
			uiModel().defaultUnitStyles.captionClass.trim()
		}
	}

	op String contentClass() {
		if (contentClassOverride.trim() !== '') {
			contentClassOverride.trim()
		} else if (uiModel().defaultEditStyles.contentClass.trim() !== '') {
			uiModel().defaultEditStyles.contentClass.trim()
		} else {
			uiModel().defaultUnitStyles.contentClass.trim()
		}
	}

	op String controlClass() {
		if (controlClassOverride.trim() !== '') {
			controlClassOverride.trim()
		} else if (uiModel().defaultEditStyles.controlClass.trim() !== '') {
			uiModel().defaultEditStyles.controlClass.trim()
		} else {
			uiModel().defaultUnitStyles.controlClass.trim()
		}
	}

	op String fieldDefinitionListClass() {
		if (fieldDefinitionListClassOverride.trim() !== '') {
			fieldDefinitionListClassOverride.trim()
		} else if (uiModel().defaultEditStyles.fieldDefinitionListClass.trim() !== '') {
			uiModel().defaultEditStyles.fieldDefinitionListClass.trim()
		} else {
			uiModel().defaultUnitStyles.fieldDefinitionListClass.trim()
		}
	}

	op String fieldLabelClass() {
		if (fieldLabelClassOverride.trim() !== '') {
			fieldLabelClassOverride.trim()
		} else if (uiModel().defaultEditStyles.fieldLabelClass.trim() !== '') {
			uiModel().defaultEditStyles.fieldLabelClass.trim()
		} else {
			uiModel().defaultUnitStyles.fieldLabelClass.trim()
		}
	}

	op String fieldListClass() {
		if (fieldListClassOverride.trim() !== '') {
			fieldListClassOverride.trim()
		} else if (uiModel().defaultEditStyles.fieldListClass.trim() !== '') {
			uiModel().defaultEditStyles.fieldListClass.trim()
		} else {
			uiModel().defaultUnitStyles.fieldListClass.trim()
		}
	}

	op String fieldValueClass() {
		if (fieldValueClassOverride.trim() !== '') {
			fieldValueClassOverride.trim()
		} else if (uiModel().defaultEditStyles.fieldValueClass.trim() !== '') {
			uiModel().defaultEditStyles.fieldValueClass.trim()
		} else {
			uiModel().defaultUnitStyles.fieldValueClass.trim()
		}
	}

	op String inputGroupClass() {
		if (inputGroupClassOverride.trim() !== '') {
			inputGroupClassOverride.trim()
		} else {
			uiModel().defaultEditStyles.inputGroupClass.trim()
		}
	}
	op boolean hasInputGroupClass() {
		inputGroupClass() !== ''
	}

	op String sectionClass() {
		if (sectionClassOverride.trim() !== '') {
			sectionClassOverride.trim()
		} else if (uiModel().defaultEditStyles.sectionClass.trim() !== '') {
			uiModel().defaultEditStyles.sectionClass.trim()
		} else {
			uiModel().defaultUnitStyles.sectionClass.trim()
		}
	}
}

class CreateUnit extends EditUnit {
}

class UpdateUnit extends EditUnit, SelectableUnit {
	boolean displayEmptyForm = "true"
}

class CreateUpdateUnit extends EditUnit, SelectableUnit {
	String updateUriElement = ''
	String clearLabel
}

class MapUnit extends EditUnit, SelectableUnit {
	boolean readOnly
	int defaultZoomLevel
	refers LocationFeature[1] location
	refers Attribute placeName
}

class DetailsUnit extends SingletonUnit, SelectableUnit {
	@GenModel(propertyCategory="Business")
	refers Selection selection
	@GenModel(propertyCategory="Interface")
	boolean omitFieldLabels

	op String actionClass() {
		if (actionClassOverride.trim() !== '') {
			actionClassOverride.trim()
		} else if (uiModel().defaultDetailsStyles.actionClass.trim() !== '') {
			uiModel().defaultDetailsStyles.actionClass.trim()
		} else {
			uiModel().defaultUnitStyles.actionClass.trim()
		}
	}

	op String captionClass() {
		if (captionClassOverride.trim() !== '') {
			contentClassOverride.trim()
		} else if (uiModel().defaultDetailsStyles.captionClass.trim() !== '') {
			uiModel().defaultDetailsStyles.captionClass.trim()
		} else {
			uiModel().defaultUnitStyles.captionClass.trim()
		}
	}

	op String contentClass() {
		if (contentClassOverride.trim() !== '') {
			contentClassOverride.trim()
		} else if (uiModel().defaultDetailsStyles.contentClass.trim() !== '') {
			uiModel().defaultDetailsStyles.contentClass.trim()
		} else {
			uiModel().defaultUnitStyles.contentClass.trim()
		}
	}

	op String controlClass() {
		if (controlClassOverride.trim() !== '') {
			controlClassOverride.trim()
		} else if (uiModel().defaultDetailsStyles.controlClass.trim() !== '') {
			uiModel().defaultDetailsStyles.controlClass.trim()
		} else {
			uiModel().defaultUnitStyles.controlClass.trim()
		}
	}

	op String definitionFieldListClass() {
		if (fieldDefinitionListClassOverride.trim() !== '') {
			fieldDefinitionListClassOverride.trim()
		} else if (uiModel().defaultDetailsStyles.fieldDefinitionListClass.trim() !== '') {
			uiModel().defaultDetailsStyles.fieldDefinitionListClass.trim()
		} else {
			uiModel().defaultUnitStyles.fieldDefinitionListClass.trim()
		}
	}

	op String fieldLabelClass() {
		if (fieldLabelClassOverride.trim() !== '') {
			fieldLabelClassOverride.trim()
		} else if (uiModel().defaultDetailsStyles.fieldLabelClass.trim() !== '') {
			uiModel().defaultDetailsStyles.fieldLabelClass.trim()
		} else {
			uiModel().defaultUnitStyles.fieldLabelClass.trim()
		}
	}

	op String fieldListClass() {
		if (fieldListClassOverride.trim() !== '') {
			fieldListClassOverride.trim()
		} else if (uiModel().defaultDetailsStyles.fieldListClass.trim() !== '') {
			uiModel().defaultDetailsStyles.fieldListClass.trim()
		} else {
			uiModel().defaultUnitStyles.fieldListClass.trim()
		}
	}

	op String fieldValueClass() {
		if (fieldValueClassOverride.trim() !== '') {
			fieldValueClassOverride.trim()
		} else if (uiModel().defaultDetailsStyles.fieldValueClass.trim() !== '') {
			uiModel().defaultDetailsStyles.fieldValueClass.trim()
		} else {
			uiModel().defaultUnitStyles.fieldValueClass.trim()
		}
	}

	op String sectionClass() {
		if (sectionClassOverride.trim() !== '') {
			sectionClassOverride.trim()
		} else if (uiModel().defaultDetailsStyles.sectionClass.trim() !== '') {
			uiModel().defaultDetailsStyles.sectionClass.trim()
		} else {
			uiModel().defaultUnitStyles.sectionClass.trim()
		}
	}
}

class TabularUnit extends CollectionUnit {
	@GenModel(propertyCategory="Debug")
	refers SearchUnit targettingSearches opposite resultsDestination

	@GenModel(propertyCategory="Style")
	String footerClassOverride = ''
	@GenModel(propertyCategory="Style")
	String headerClassOverride = ''
	@GenModel(propertyCategory="Style")
	String rowClassesOverride

	op String actionClass() {
		if (actionClassOverride.trim() !== '') {
			actionClassOverride.trim()
		} else if (uiModel().defaultTabularStyles.actionClass.trim() !== '') {
			uiModel().defaultTabularStyles.actionClass.trim()
		} else if (uiModel().defaultCollectionStyles.actionClass.trim() !== '') {
			uiModel().defaultCollectionStyles.actionClass.trim()
		} else {
			uiModel().defaultUnitStyles.actionClass.trim()
		}
	}

	op String captionClass() {
		if (captionClassOverride.trim() !== '') {
			contentClassOverride.trim()
		} else if (uiModel().defaultTabularStyles.captionClass.trim() !== '') {
			uiModel().defaultTabularStyles.captionClass.trim()
		} else if (uiModel().defaultCollectionStyles.captionClass.trim() !== '') {
			uiModel().defaultCollectionStyles.captionClass.trim()
		} else {
			uiModel().defaultUnitStyles.captionClass.trim()
		}
	}

	op String contentClass() {
		if (contentClassOverride.trim() !== '') {
			contentClassOverride.trim()
		} else if (uiModel().defaultTabularStyles.contentClass.trim() !== '') {
			uiModel().defaultTabularStyles.contentClass.trim()
		} else if (uiModel().defaultCollectionStyles.contentClass.trim() !== '') {
			uiModel().defaultCollectionStyles.contentClass.trim()
		} else {
			uiModel().defaultUnitStyles.contentClass.trim()
		}
	}

	op String controlClass() {
		if (controlClassOverride.trim() !== '') {
			controlClassOverride.trim()
		} else if (uiModel().defaultTabularStyles.controlClass.trim() !== '') {
			uiModel().defaultTabularStyles.controlClass.trim()
		} else if (uiModel().defaultCollectionStyles.controlClass.trim() !== '') {
			uiModel().defaultCollectionStyles.controlClass.trim()
		} else {
			uiModel().defaultUnitStyles.controlClass.trim()
		}
	}

	op String fielddefinitionListClass() {
		if (fieldDefinitionListClassOverride.trim() !== '') {
			fieldDefinitionListClassOverride.trim()
		} else if (uiModel().defaultTabularStyles.fieldDefinitionListClass.trim() !== '') {
			uiModel().defaultTabularStyles.fieldDefinitionListClass.trim()
		} else if (uiModel().defaultCollectionStyles.fieldDefinitionListClass.trim() !== '') {
			uiModel().defaultCollectionStyles.fieldDefinitionListClass.trim()
		} else {
			uiModel().defaultUnitStyles.fieldDefinitionListClass.trim()
		}
	}

	op String fieldLabelClass() {
		if (fieldLabelClassOverride.trim() !== '') {
			fieldLabelClassOverride.trim()
		} else if (uiModel().defaultTabularStyles.fieldLabelClass.trim() !== '') {
			uiModel().defaultTabularStyles.fieldLabelClass.trim()
		} else if (uiModel().defaultCollectionStyles.fieldLabelClass.trim() !== '') {
			uiModel().defaultCollectionStyles.fieldLabelClass.trim()
		} else {
			uiModel().defaultUnitStyles.fieldLabelClass.trim()
		}
	}

	op String fieldListClass() {
		if (fieldListClassOverride.trim() !== '') {
			fieldListClassOverride.trim()
		} else if (uiModel().defaultTabularStyles.fieldListClass.trim() !== '') {
			uiModel().defaultTabularStyles.fieldListClass.trim()
		} else if (uiModel().defaultCollectionStyles.fieldListClass.trim() !== '') {
			uiModel().defaultCollectionStyles.fieldListClass.trim()
		} else {
			uiModel().defaultUnitStyles.fieldListClass.trim()
		}
	}

	op String fieldValueClass() {
		if (fieldValueClassOverride.trim() !== '') {
			fieldValueClassOverride.trim()
		} else if (uiModel().defaultTabularStyles.fieldValueClass.trim() !== '') {
			uiModel().defaultTabularStyles.fieldValueClass.trim()
		} else if (uiModel().defaultCollectionStyles.fieldValueClass.trim() !== '') {
			uiModel().defaultCollectionStyles.fieldValueClass.trim()
		} else {
			uiModel().defaultUnitStyles.fieldValueClass.trim()
		}
	}

	op String filtersClass() {
		if (filtersClassOverride.trim() !== '') {
			filtersClassOverride.trim()
		} else if (uiModel().defaultTabularStyles.filtersClass.trim() !== '') {
			uiModel().defaultTabularStyles.filtersClass.trim()
		} else {
			uiModel().defaultCollectionStyles.filtersClass.trim()
		}
	}

	op String filtersControlClass() {
		if (filtersControlClassOverride.trim() !== '') {
			filtersControlClassOverride.trim()
		} else if (uiModel().defaultTabularStyles.filtersControlClass.trim() !== '') {
			uiModel().defaultTabularStyles.filtersControlClass.trim()
		} else {
			uiModel().defaultCollectionStyles.filtersControlClass.trim()
		}
	}

	op String filtersActionClass() {
		if (filtersActionClassOverride.trim() !== '') {
			filtersActionClassOverride.trim()
		} else if (uiModel().defaultTabularStyles.filtersActionClass.trim() !== '') {
			uiModel().defaultTabularStyles.filtersActionClass.trim()
		} else {
			uiModel().defaultCollectionStyles.filtersActionClass.trim()
		}
	}

	op String footerClass() {
		if (footerClassOverride.trim() !== '') {
			footerClassOverride.trim()
		} else {
			uiModel().defaultTabularStyles.footerClass.trim()
		}
	}
	op boolean hasFooterClass() {
		footerClass() !== ''
	}

	op String headerClass() {
		if (headerClassOverride.trim() !== '') {
			headerClassOverride.trim()
		} else {
			uiModel().defaultTabularStyles.headerClass.trim()
		}
	}
	op boolean hasHeaderClass() {
		headerClass() !== ''
	}

	op String rowClasses() {
		if (rowClassesOverride.trim() !== '') {
			rowClassesOverride.trim()
		} else {
			uiModel().defaultTabularStyles.rowClasses.trim()
		}
	}
	op boolean hasRowClasses() {
		rowClasses() !== ''
	}

	op String sectionClass() {
		if (sectionClassOverride.trim() !== '') {
			sectionClassOverride.trim()
		} else if (uiModel().defaultTabularStyles.sectionClass.trim() !== '') {
			uiModel().defaultTabularStyles.sectionClass.trim()
		} else if (uiModel().defaultCollectionStyles.sectionClass.trim() !== '') {
			uiModel().defaultCollectionStyles.sectionClass.trim()
		} else {
			uiModel().defaultUnitStyles.sectionClass.trim()
		}
	}
}

abstract class CardsUnit extends CollectionUnit {
	contains Badge[] badges opposite displayedOn
	boolean overlaySingleSelectAction

	@GenModel(propertyCategory="Style")
	String[1] badgeClassOverride = ''
	@GenModel(propertyCategory="Style")
	String[1] cardClassOverride = ''
	@GenModel(propertyCategory="Style")
	String[1] cardBodyClassOverride = ''
	@GenModel(propertyCategory="Style")
	String[1] cardFooterClassOverride = ''
	@GenModel(propertyCategory="Style")
	String[1] cardHeaderClassOverride = ''

	op String actionClass() {
		if (actionClassOverride.trim() !== '') {
			actionClassOverride.trim()
		} else if (uiModel().defaultCardsStyles.actionClass.trim() !== '') {
			uiModel().defaultCardsStyles.actionClass.trim()
		} else if (uiModel().defaultCollectionStyles.actionClass.trim() !== '') {
			uiModel().defaultCollectionStyles.actionClass.trim()
		} else {
			uiModel().defaultUnitStyles.actionClass.trim()
		}
	}

	op String badgeClass() {
		if (badgeClassOverride.trim() !== '') {
			badgeClassOverride.trim()
		} else {
			uiModel().defaultCardsStyles.badgeClass.trim()
		}
	}
	op boolean hasBadgeClass() {
		badgeClass() !== ''
	}

	op String captionClass() {
		if (captionClassOverride.trim() !== '') {
			contentClassOverride.trim()
		} else if (uiModel().defaultCardsStyles.captionClass.trim() !== '') {
			uiModel().defaultCardsStyles.captionClass.trim()
		} else if (uiModel().defaultCollectionStyles.captionClass.trim() !== '') {
			uiModel().defaultCollectionStyles.captionClass.trim()
		} else {
			uiModel().defaultUnitStyles.captionClass.trim()
		}
	}

	op String cardClass() {
		if (cardClassOverride.trim() !== '') {
			cardClassOverride.trim()
		} else {
			uiModel().defaultCardsStyles.cardClass.trim()
		}
	}
	op boolean hasCardClass() {
		cardClass() !== ''
	}

	op String cardFooterClass() {
		if (cardFooterClassOverride.trim() !== '') {
			cardFooterClassOverride.trim()
		} else {
			uiModel().defaultCardsStyles.cardFooterClass.trim()
		}
	}
	op boolean hasCardFooterClass() {
		cardFooterClass() !== ''
	}

	op String cardBodyClass() {
		if (cardBodyClassOverride.trim() !== '') {
			cardBodyClassOverride.trim()
		} else {
			uiModel().defaultCardsStyles.cardBodyClass.trim()
		}
	}
	op boolean hasCardBodyClass() {
		cardBodyClass() !== ''
	}

	op String cardHeaderClass() {
		if (cardHeaderClassOverride.trim() !== '') {
			cardHeaderClassOverride.trim()
		} else {
			uiModel().defaultCardsStyles.cardHeaderClass.trim()
		}
	}
	op boolean hasCardHeaderClass() {
		cardHeaderClass() !== ''
	}

	op String contentClass() {
		if (contentClassOverride.trim() !== '') {
			contentClassOverride.trim()
		} else if (uiModel().defaultCardsStyles.contentClass.trim() !== '') {
			uiModel().defaultCardsStyles.contentClass.trim()
		} else if (uiModel().defaultCollectionStyles.contentClass.trim() !== '') {
			uiModel().defaultCollectionStyles.contentClass.trim()
		} else {
			uiModel().defaultUnitStyles.contentClass.trim()
		}
	}

	op String controlClass() {
		if (controlClassOverride.trim() !== '') {
			controlClassOverride.trim()
		} else if (uiModel().defaultCardsStyles.controlClass.trim() !== '') {
			uiModel().defaultCardsStyles.controlClass.trim()
		} else if (uiModel().defaultCollectionStyles.controlClass.trim() !== '') {
			uiModel().defaultCollectionStyles.controlClass.trim()
		} else {
			uiModel().defaultUnitStyles.controlClass.trim()
		}
	}

	op String definitionFieldListClass() {
		if (fieldDefinitionListClassOverride.trim() !== '') {
			fieldDefinitionListClassOverride.trim()
		} else if (uiModel().defaultCardsStyles.fieldDefinitionListClass.trim() !== '') {
			uiModel().defaultCardsStyles.fieldDefinitionListClass.trim()
		} else if (uiModel().defaultCollectionStyles.fieldDefinitionListClass.trim() !== '') {
			uiModel().defaultCollectionStyles.fieldDefinitionListClass.trim()
		} else {
			uiModel().defaultUnitStyles.fieldDefinitionListClass.trim()
		}
	}

	op String fieldLabelClass() {
		if (fieldLabelClassOverride.trim() !== '') {
			fieldLabelClassOverride.trim()
		} else if (uiModel().defaultCardsStyles.fieldLabelClass.trim() !== '') {
			uiModel().defaultCardsStyles.fieldLabelClass.trim()
		} else if (uiModel().defaultCollectionStyles.fieldLabelClass.trim() !== '') {
			uiModel().defaultCollectionStyles.fieldLabelClass.trim()
		} else {
			uiModel().defaultUnitStyles.fieldLabelClass.trim()
		}
	}

	op String fieldListClass() {
		if (fieldListClassOverride.trim() !== '') {
			fieldListClassOverride.trim()
		} else if (uiModel().defaultCardsStyles.fieldListClass.trim() !== '') {
			uiModel().defaultCardsStyles.fieldListClass.trim()
		} else if (uiModel().defaultCollectionStyles.fieldListClass.trim() !== '') {
			uiModel().defaultCollectionStyles.fieldListClass.trim()
		} else {
			uiModel().defaultUnitStyles.fieldListClass.trim()
		}
	}

	op String fieldValueClass() {
		if (fieldValueClassOverride.trim() !== '') {
			fieldValueClassOverride.trim()
		} else if (uiModel().defaultCardsStyles.fieldValueClass.trim() !== '') {
			uiModel().defaultCardsStyles.fieldValueClass.trim()
		} else if (uiModel().defaultCollectionStyles.fieldValueClass.trim() !== '') {
			uiModel().defaultCollectionStyles.fieldValueClass.trim()
		} else {
			uiModel().defaultUnitStyles.fieldValueClass.trim()
		}
	}

	op String filtersClass() {
		if (filtersClassOverride.trim() !== '') {
			filtersClassOverride.trim()
		} else if (uiModel().defaultCardsStyles.filtersClass.trim() !== '') {
			uiModel().defaultCardsStyles.filtersClass.trim()
		} else {
			uiModel().defaultCollectionStyles.filtersClass.trim()
		}
	}

	op String filtersControlClass() {
		if (filtersControlClassOverride.trim() !== '') {
			filtersControlClassOverride.trim()
		} else if (uiModel().defaultCardsStyles.filtersControlClass.trim() !== '') {
			uiModel().defaultCardsStyles.filtersControlClass.trim()
		} else {
			uiModel().defaultCollectionStyles.filtersControlClass.trim()
		}
	}

	op String filtersActionClass() {
		if (filtersActionClassOverride.trim() !== '') {
			filtersActionClassOverride.trim()
		} else if (uiModel().defaultCardsStyles.filtersActionClass.trim() !== '') {
			uiModel().defaultCardsStyles.filtersActionClass.trim()
		} else {
			uiModel().defaultCollectionStyles.filtersActionClass.trim()
		}
	}

	op String instanceActionClass() {
		if (instanceActionClassOverride.trim() !== '') {
			instanceActionClassOverride.trim()
		} else if (uiModel().defaultCardsStyles.instanceActionClass.trim() !== '') {
			uiModel().defaultCardsStyles.instanceActionClass.trim()
		} else {
			uiModel().defaultCollectionStyles.instanceActionClass.trim()
		}
	}

	op String instanceControlClass() {
		if (instanceControlClassOverride.trim() !== '') {
			instanceControlClassOverride.trim()
		} else if (uiModel().defaultCardsStyles.instanceControlClass.trim() !== '') {
			uiModel().defaultCardsStyles.instanceControlClass.trim()
		} else {
			uiModel().defaultCollectionStyles.instanceControlClass.trim()
		}
	}

	op String sectionClass() {
		if (sectionClassOverride.trim() !== '') {
			sectionClassOverride.trim()
		} else if (uiModel().defaultCardsStyles.sectionClass.trim() !== '') {
			uiModel().defaultCardsStyles.sectionClass.trim()
		} else if (uiModel().defaultCollectionStyles.sectionClass.trim() !== '') {
			uiModel().defaultCollectionStyles.sectionClass.trim()
		} else {
			uiModel().defaultUnitStyles.sectionClass.trim()
		}
	}
}

class Badge extends ValueContext {
	container CardsUnit[1] displayedOn opposite badges

	@GenModel(propertyCategory="Interface")
	refers Attribute title
	@GenModel(propertyCategory="Interface")
	String iconName
	@GenModel(propertyCategory="Interface")
	contains DisplayValue value

	contains Predicate displayWhen

	@GenModel(propertyCategory="Style")
	String bdgeClassOverride

	op String badgeClass() {
		if (bdgeClassOverride.trim() !== '') {
			bdgeClassOverride.trim()
		} else {
			displayedOn.badgeClass()
		}
	}
	op boolean hasBadgeClass() {
		badgeClass() !== ''
	}
}

class TextCardsUnit extends CardsUnit {
}

class DateCardsUnit extends CardsUnit, ValueContext {
	contains DisplayValue[1] elementDate
}

interface ImageUnit extends ValueContext {
	contains DisplayValue[1] imageFeaturePath

	@GenModel(propertyCategory="Image")
	refers ImageManipulation imageFilter

	@GenModel(propertyCategory="Image")
	String missingImagePath
	contains Predicate showMissingImageWhen
}


class ImageCardsUnit extends CardsUnit, ImageUnit {
	@GenModel(propertyCategory="Interface")
	boolean overlayTitle

	@GenModel(propertyCategory="Interface")
	boolean enableImageEnlargement
}

class SliderUnit extends CollectionUnit, ImageUnit {
	@GenModel(propertyCategory="Interface")
	int showTime
	@GenModel(propertyCategory="Interface")
	int transitionTime

}

class GalleryUnit extends CollectionUnit, ImageUnit {
	@GenModel(propertyCategory="Interface")
	int showTime
	@GenModel(propertyCategory="Interface")
	int transitionTime

	@GenModel(propertyCategory="Image")
	refers ImageManipulation fullSizeFilter
}

abstract class ControlUnit extends DynamicUnit {
	String[1] submitLabel
	refers ContentUnit cancelDestination
	String cancelLabel

	op String actionClass() {
		if (actionClassOverride.trim() !== '') {
			actionClassOverride.trim()
		} else if (uiModel().defaultControlStyles.actionClass.trim() !== '') {
			uiModel().defaultControlStyles.actionClass.trim()
		} else {
			uiModel().defaultUnitStyles.actionClass.trim()
		}
	}

	op String captionClass() {
		if (captionClass.trim() !== '') {
			contentClass.trim()
		} else if (uiModel().defaultControlStyles.captionClass.trim() !== '') {
			uiModel().defaultControlStyles.captionClass.trim()
		} else {
			uiModel().defaultUnitStyles.captionClass.trim()
		}
	}

	op String contentClass() {
		if (contentClassOverride.trim() !== '') {
			contentClassOverride.trim()
		} else if (uiModel().defaultControlStyles.contentClass.trim() !== '') {
			uiModel().defaultControlStyles.contentClass.trim()
		} else {
			uiModel().defaultUnitStyles.contentClass.trim()
		}
	}

	op String controlClass() {
		if (controlClassOverride.trim() !== '') {
			controlClassOverride.trim()
		} else if (uiModel().defaultControlStyles.controlClass.trim() !== '') {
			uiModel().defaultControlStyles.controlClass.trim()
		} else {
			uiModel().defaultUnitStyles.controlClass.trim()
		}
	}

	op String fieldListClass() {
		if (fieldListClassOverride.trim() !== '') {
			fieldListClassOverride.trim()
		} else if (uiModel().defaultControlStyles.fieldListClass.trim() !== '') {
			uiModel().defaultControlStyles.fieldListClass.trim()
		} else {
			uiModel().defaultUnitStyles.fieldListClass.trim()
		}
	}

	op String fieldDefinitionListClass() {
		if (fieldDefinitionListClassOverride.trim() !== '') {
			fieldDefinitionListClassOverride.trim()
		} else if (uiModel().defaultControlStyles.fieldDefinitionListClass.trim() !== '') {
			uiModel().defaultControlStyles.fieldDefinitionListClass.trim()
		} else {
			uiModel().defaultUnitStyles.fieldDefinitionListClass.trim()
		}
	}

	op String fieldLabelClass() {
		if (fieldLabelClassOverride.trim() !== '') {
			fieldLabelClassOverride.trim()
		} else if (uiModel().defaultControlStyles.fieldLabelClass.trim() !== '') {
			uiModel().defaultControlStyles.fieldLabelClass.trim()
		} else {
			uiModel().defaultUnitStyles.fieldLabelClass.trim()
		}
	}

	op String fieldValueClass() {
		if (fieldValueClassOverride.trim() !== '') {
			fieldValueClassOverride.trim()
		} else if (uiModel().defaultControlStyles.fieldValueClass.trim() !== '') {
			uiModel().defaultControlStyles.fieldValueClass.trim()
		} else {
			uiModel().defaultUnitStyles.fieldValueClass.trim()
		}
	}

	op String sectionClass() {
		if (sectionClassOverride.trim() !== '') {
			sectionClassOverride.trim()
		} else if (uiModel().defaultControlStyles.sectionClass.trim() !== '') {
			uiModel().defaultControlStyles.sectionClass.trim()
		} else {
			uiModel().defaultUnitStyles.sectionClass.trim()
		}
	}
}

class SearchUnit extends ControlUnit {
	refers TabularUnit[1] resultsDestination opposite targettingSearches
}


interface ValueContext {
	op Entity[] referencableTypes()
}

abstract class DisplayValue extends Path {
	@OCL(derivation="if leafFeature().oclIsUndefined() then
			false
		else
			leafFeature().isRequired
		endif")
    @GenModel(propertyCategory="Debug")
	derived readonly transient volatile boolean isRequired

	@OCL(derivation="if leafFeature().oclIsUndefined() then
			false
		else
			leafFeature().isSingleton
		endif")
    @GenModel(propertyCategory="Debug")
	derived readonly transient volatile boolean isSingleton
}

class AssociationDisplayValue extends AssociationPathElement, DisplayValue {
}

class AttributeDisplayValue extends AttributePathElement, DisplayValue {
}

class ResourceDisplayValue extends ResourcePathElement, DisplayValue {
}

class LabelDisplayValue extends DisplayValue {
	refers ModelLabel[1] label

	op String name() {
		if (label === null ) {
			''
		} else {
			label.name
		}
	}
	
	op Path leafElement() {
		this
	}
}


enum ContainerActionsPlacement {
	Above = 0
	Below = 1
	Float = 2
}

enum InstanceActionsPlacement {
	Footer = 0
	Float = 1
}

interface ActionContainer {
    @GenModel(propertyCategory="Interface")
	ContainerActionsPlacement containerActionsPlacement

    @GenModel(propertyCategory="Interface")
	InstanceActionsPlacement instanceActionsPlacement

	contains Action[] allActions opposite usedBy

	op Action[] actions() {
		allActions.filter[!it.disable].toEList()
	}

	op boolean hasActions() {
		!actions().isEmpty()
	}

	op boolean hasActionsWithDisabledMessage() {
		!actions().filter[it.hasDisabledMessage()].isEmpty()
	}

	op boolean hasActionsWithSuccessMessage() {
		!actions().filter[it.hasSuccessMessage()].isEmpty()
	}

	op boolean hasActionsWithMessages() {
		!actions().filter[it.hasMessages()].isEmpty()
	}

	op Action[] actionsWithoutTarget() {
		actions().filter[!it.isSelectAction()].asEList()
	}

	op Action[] containerActions() {
		actions().filter[it.isContainerAction()].asEList()
	}
	op boolean hasContainerActions() {
		!containerActions().isEmpty()
	}

	op OperationAction[] fileActions() {
		operationActions.filter[it.operation.resultType.literal == 'File'].toEList()
	}
	op boolean hasFileActions() {
		!fileActions().isEmpty()
	}

	op Action[] generalActions() {
		actions().filter[it.isGeneralAction()].asEList()
	}
	op boolean hasGeneralActions() {
		!generalActions().isEmpty()
	}

	op Action[] targetActions() {
		actions().filter[it.isTargetAction()].asEList()
	}
	op boolean hasTargetActions() {
		!targetActions().isEmpty()
	}

	op Action[] instanceActions() {
		actions().filter[it.isInstanceAction()].asEList()
	}
	op boolean hasInstanceActions() {
		!instanceActions().isEmpty()
	}

	op OperationAction[] operationActions() {
		actions().filter[it.isOperationAction()].map[it as OperationAction].asEList()
	}
	op boolean hasOperationActions() {
		!operationActions().isEmpty()
	}
}

abstract class Action extends NamedDisplayElement {
	refers ActionContainer[1]usedBy opposite allActions

    @GenModel(propertyCategory="Interface")
	String iconName = ''

    @GenModel(propertyCategory="Business")
	boolean disable

	contains Predicate displayWhen

	contains Predicate enableWhen

	op DynamicUnit parentUnit() {
		if (usedBy instanceof DynamicUnit) {
			usedBy as DynamicUnit
		} else if (usedBy instanceof UnitFeature) {
			(usedBy as UnitFeature).displayedOn
		} else {
			return null
		}
	}

	op AuthorisationExpression authorisation() {
		null
	}

	op boolean isAuthorised() {
		authorisation() !== null
	}

	op boolean hasCustomisedAuthorisation() {
		if (authorisation() === null) {
			false
		} else if (parentUnit().authorisation === null) {
			true
		} else {
			!authorisation().areSame(parentUnit().authorisation)
		}
	}

	op boolean isContainerAction() {
		false
	}

	op boolean isGeneralAction() {
		false
	}

	op boolean isInstanceAction() {
		false
	}

	op boolean isOperationAction() {
		false
	}

	op boolean isSelectAction() {
		false
	}

	op boolean isTargetAction() {
		false
	}

    @GenModel(propertyCategory="Interface")
	String confirmMessage = ''
	op boolean hasConfirmMessage() {
		confirmMessage.trim() !== ''
	}

    @GenModel(propertyCategory="Interface")
	String successMessage = ''
	op boolean hasSuccessMessage() {
		successMessage.trim() !== ''
	}

    @GenModel(propertyCategory="Interface")
	String failureMessage = ''
	op boolean hasFailureMessage() {
		failureMessage.trim() !== ''
	}

    @GenModel(propertyCategory="Interface")
	String disabledMessage = ''
	op boolean hasDisabledMessage() {
		disabledMessage.trim() !== ''
	}

	op boolean hasMessages() {
		hasConfirmMessage() || hasSuccessMessage() || hasFailureMessage() || hasDisabledMessage()
	}

    @GenModel(propertyCategory="Style")
	String localActionClass = ''
	op String actionClass() {
		if (localActionClass.trim() !== '') {
			localActionClass.trim()
		} else if (usedBy instanceof DynamicUnit) {
			if (isInstanceAction()) {
				if (parentUnit() instanceof CollectionUnit) {
					if ((parentUnit() as CollectionUnit).hasInstanceActionClass()) {
						(parentUnit() as CollectionUnit).instanceActionClass()
					} else {
						''
					}
				} else {
					''
				}
			} else if (parentUnit().hasActionClass()) {
				parentUnit().actionClass()
			} else {
				''
			}
		} else {
			''
		}
	}
	op boolean hasActionClass() {
		actionClass() !== ''
	}
}

class DeleteAction extends Action {
    @GenModel(propertyCategory="Navigation")
	String[1] uriElement

	contains AuthorisationExpression localAuthorisation

	op AuthorisationExpression authorisation() {
		if (localAuthorisation !== null) {
			localAuthorisation
		} else {
			parentUnit()?.authorisation
		}
	}

	op boolean isInstanceAction() {
		true
	}
}

abstract class SelectAction extends Action {
    @GenModel(propertyCategory="Navigation")
	refers SelectableUnit[1] target opposite selectors
	contains DisplayValue valuePath

	op AuthorisationExpression authorisation() {
		if (target === null) {
			null
		} else {
			(target as DynamicUnit).authorisation
		}
	}

	op boolean isSelectAction() {
		true
	}
}

class ContainerSelectAction extends SelectAction {
	op boolean isContainerAction() {
		true
	}
}

class InstanceSelectAction extends SelectAction {
	op boolean isInstanceAction() {
		true
	}
}

class TargetAction extends SelectAction {
	op boolean isTargetAction() {
		true
	}
}

abstract class OperationAction extends Action {
	@OCL(derivation="if operation.oclIsUndefined() then
			OrderedSet(service::Service){}
		else
			OrderedSet(service::Service){ operation.partOf }
		endif")
    @GenModel(propertyCategory="Debug")
	refers derived readonly transient volatile Service[] servicesUsed
	@OCL(derivation="servicesUsed->notEmpty()")
    @GenModel(propertyCategory="Debug")
	derived readonly unsettable transient volatile boolean hasServicesUsed

    @GenModel(propertyCategory="Business")
	refers BusinessOperation[1] operation

    @GenModel(propertyCategory="Navigation")
	String[1] uriElement

	contains AuthorisationExpression localAuthorisation

	op AuthorisationExpression authorisation() {
		if (localAuthorisation !== null) {
			localAuthorisation
		} else {
			parentUnit()?.authorisation
		}
	}

	op boolean isOperationAction() {
		true
	}
}

class InstanceOperationAction extends OperationAction {
	op boolean isInstanceAction() {
		true
	}
}

class ContainerOperationAction extends OperationAction {
}

class GeneralOperationAction extends OperationAction {
	op boolean isGeneralAction() {
		true
	}
}


class Message extends ValueContext {
	String[1] text
	contains DisplayValue[] features
}
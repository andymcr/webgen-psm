@Ecore(
	nsURI="http://www.cs.man.ac.uk/mdsd/2010/ObjectRelationalMapping",
	nsPrefix="orm"
)
@Ecore(
	validationDelegates="http://www.eclipse.org/emf/2002/Ecore/OCL"
)
@GenModel(
	editDirectory="/uk.ac.man.cs.mdsd.orm.edit/src",
	editPluginID="uk.ac.man.cs.mdsd.orm.edit",
	childCreationExtenders="true",
	complianceLevel="6.0")
package uk.ac.man.cs.mdsd.orm

import uk.ac.man.cs.mdsd.criteria.Expression

annotation "http://www.eclipse.org/emf/2002/Ecore/OCL" as OCL

class OrmModel {
	contains DataType[] dataTypes
	contains EntityOrView[] entitiesAndViews opposite partOf
	contains CollectionType[] collectionTypes
	DatabaseTechnologies databaseTechnology = "MySql"
	String databasePrefix
	String databaseHost
	String databasePort
	String databaseName
	String databaseUsername
	String databasePassword
	OrmTechnologies ormTechnology
	boolean timestampCreation
	boolean timestampUpdates
	boolean hasCustomOrmTypes
}

enum Cardinality {
	Optional
	Required = 1
	Many = 2
}

enum DatabaseTechnologies {
	MySql
	Oracle = 1
}

enum IsHasChoices {
	isA
	hasA = 1
}

enum OrmTechnologies {
	JPA
	DataMapper = 1
	Idiorm = 2
	Kohana = 3
	DoctrineORM = 4
	DoctrineODM = 5
}

abstract class NamedElement {
	@GenModel(property="None")
	String[1] name
}

abstract class NamedDisplayElement extends NamedElement {
	@GenModel(property="None")
	String[1] displayLabel
}

abstract class Classifier extends NamedDisplayElement {
}

enum CollectionTypes {
	Bag
	List
	OrderedSet
	Set
}

class CollectionType extends NamedElement {
	CollectionTypes collectionType
	refers Classifier[1] elementType
	String[1] persistenceType
	String[1] javaInterfaceType
	String[1] javaImplementationType
}

class DataType extends Classifier {
}

class EnumerationType extends DataType {
	contains EnumerationLiteral[] enumerations
}

class EnumerationLiteral extends NamedDisplayElement {
}


abstract class EntityOrView extends Classifier {
	container OrmModel[1] partOf opposite entitiesAndViews
	contains Feature[] features opposite partOf
	refers Feature[] ^keys
	String tableName
	String autoKeyName
	String autoKeyPersistentType
	String autoKeyGenerationStrategy
	contains ModelLabel[] labels opposite labelFor
	boolean[1] serializationExcludeAll = "false"
	boolean implementsUserInterface = "false"
}


abstract class Feature extends NamedDisplayElement {
	container EntityOrView[1] partOf opposite features
	boolean[1] encodeUriKey = "false"
	boolean[1] serializationExpose = "true"
	String[] serializationGroups
}

abstract class Attribute extends Feature, Label {
	contains Expression defaultValue
	String validationPattern
}

abstract class Association extends Feature {
	boolean pseudo
	Integer[1] serializationMaxDepth = "1"
}

interface Label {
}

class ModelLabel extends NamedElement, Label {
	container EntityOrView[1] labelFor opposite labels
	String[1] format
	contains ModelLabelFeature[] features
}

abstract class ModelLabelFeature {
}

class ModelLabelAttribute extends ModelLabelFeature {
	refers Attribute[1] attribute
	String[1] dateFormat
}

class ModelLabelAssociation extends ModelLabelFeature {
	refers Association[1] association
	refers ModelLabel dynamicLabel
}


class Entity extends EntityOrView {
}

abstract class EntityFeature extends Feature {
	boolean primaryKey
	Cardinality cardinality
	IsHasChoices booleanIsHasChoice
	boolean collectionAllowAdd
	boolean collectionAllowRemove
	String[1] columnName
}

abstract class EntityAttribute extends EntityFeature, Attribute {
	String[1] persistentType
	String[1] ormType
}

abstract class SingletonAttribute extends EntityAttribute {
	boolean required
	boolean ^unique
}

abstract class CollectionAttribute extends EntityAttribute {
}

class SingletonElement extends SingletonAttribute {
	refers DataType[1] dataType
	boolean obfuscateFormFields
	boolean caseInsensitive
	boolean encrypt
}

class CollectionElement extends CollectionAttribute {
	refers CollectionType[1] collectionType
}

enum DateDetails {
	DateOnly = 1
	TimeOnly = 2
	DateAndTime = 3
}

interface DateAttribute {
	DateDetails details
}

class SingletonDate extends SingletonAttribute, DateAttribute {
}

class CollectionDate extends CollectionAttribute, DateAttribute {
}

class SingletonURL extends SingletonAttribute {
	String displayValue
}

abstract class SingletonResource extends SingletonAttribute {
	int maximumUploadSize
	unique String[] validUploadMimeTypes
	unique String[] validUploadExtensions
	boolean uploadsWithinWebsite
	contains PathElement[] uploadPath
}

abstract class PathElement {
}

class StaticPathElement extends PathElement {
	String[1] element
}

class DatePathElement extends PathElement {
	String[1] format
}

class SingletonFile extends SingletonResource {
}

class SingletonImage extends SingletonResource {
}

class SingletonLocation extends SingletonAttribute {
}

@Ecore(constraints="onlyOneOwningEnd")
@OCL(onlyOneOwningEnd="owningEnd xor opposite.owningEnd")
abstract class EntityAssociation extends EntityFeature, Association {
	refers EntityAssociation[1] ^opposite
	boolean[1] ^container = "false"
	boolean[1] ^contains = "false"
	boolean[1] owningEnd = "false"
	boolean[1] visible = "true"
	contains AssociationKey[] ^keys opposite keyFor
	String pivotTableName
}

enum SingletonAssociationRelationships {
	OneToOne
	ManyToOne = 1
}

class SingletonAssociation extends EntityAssociation {
	boolean required
	SingletonAssociationRelationships associationRelationship
}

enum CollectionAssociationRelationships {
	OneToMany
	ManyToMany = 1
}

@Ecore(constraints="x")
@OCL(x="collectionType.elementType = opposite.parentEntity")
class CollectionAssociation extends EntityAssociation {
	refers CollectionType[1] collectionType
	CollectionAssociationRelationships associationRelationship
}

class AssociationKey {
	container EntityAssociation[1] keyFor opposite ^keys
	refers EntityFeature[1] sourceFeature
	refers EntityFeature[1] targetFeature
}


class View extends EntityOrView {
	refers EntityOrView[] encapsulates
}

abstract class ViewFeature extends Feature {
}

abstract class EncapsulatedFeature extends ViewFeature {
	String[1] columnName
}

class EncapsulatedAttribute extends EncapsulatedFeature, Attribute {
	refers Attribute[1] attribute
}

class EncapsulatedAssociation extends EncapsulatedFeature, Association {
	refers Association[1] association
	refers EncapsulatedAssociation encapsulatedTarget
}

class ViewAssociation extends ViewFeature, Association {
	refers EncapsulatedAssociation[1] ^opposite
	Cardinality cardinality
}